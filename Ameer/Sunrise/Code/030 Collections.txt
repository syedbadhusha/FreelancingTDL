<DefType:Collection>


[Collection:EAT_CMNVCHTypes]
	Full Height	:	Yes
	Align		:	Right
	
	Format		:	$Name, 35
	
[Collection:EAT_POVCHTypes]
	Use			:	EAT_CMNVCHTypes
	
	Title		:	"List Of Purchase Order Types"
	
	List Name	:	@@EAT_PurchaseInquiry, @@EAT_PurchaseOrder
	
[Collection:EAT_PurcVCHTypes]
	Use			:	EAT_CMNVCHTypes
	
	Title		:	"List Of Purchase Types"
	
	List Name	:	@@EAT_LocalPurchase, @@EAT_ForeignPurchase
	
[Collection:EAT_SOVCHTypes]
	Use			:	EAT_CMNVCHTypes
	
	Title		:	"List Of Sales Order Types"
	
	List Name	:	@@EAT_JobCard
	
[Collection:EAT_SOVCHPrintTypes]
	Use			:	EAT_SOVCHTypes
	
	List Name	:	@@EAT_Quotation
	
[Collection:EAT_Vouchers]	
	Type		:	Vouchers
			
	Full Height	:	Yes
	Align		:	Right
	
	Keep Source	:	().
	Client Only	:	Yes
	
	Fetch		:	Amount, PartyLedgerName

[Collection:EAT_JobOrders]
	Use			:	EAT_Vouchers
	
	Title		:	"List Of Job Cards"
	
	Filter		:	EAT_IsJobCardVch
	
	Sub Title	:	"VCH No.", "Date", "Reference"
	
	Format		:	$VoucherNumber, 20
	Format		:	$Date, 9
	
[Collection:EAT_PendingDelNotes]
	Use			:	EAT_Vouchers
	
	Filter		:	IsDelNoteVch, EAT_IsEmptyLPO
	
[Collection:EAT_PurcInquiriesCMN]
	Use			:	EAT_Vouchers
	
	Title		:	"List Of Purchase Inquiries"
	
	Filter		:	EAT_IsPurcInquiryVch, EAT_NotIsTracked
	
[Collection:EAT_PurcInquiries]
	Use			:	EAT_PurcInquiriesCMN
		
	Parm Var	:	SVFromDate			:	Date		:	$StartingFrom:Company:##SVCurrentCompany
	Parm Var	:	SVToDate			:	Date		:	$Date
	Parm Var	:	Party				:	String		:	If $$IsEmpty:($$BaseOwner:$PartyLedgerName) then $LedgerEntries[1].LedgerName Else ($$BaseOwner:$PartyLedgerName)
	
	Filter		:	EAT_IsSamePartyVchs
	
	Sub Title	:	"VCH No.", "Date", "Reference"
	
	Format		:	$VoucherNumber, 25
	Format		:	$Date, 10
	Format		:	$Reference, 20
	
[Collection:EAT_SelectedInquiries]
	Source Collection	:	Default
	Walk		:	EAT_Inquiries
	
	Fetch		:	*
	
[Collection:EAT_MasterIdVCH]
	Type		:	Vouchers:MasterId
	Child Of	:	##MasterId
	
	Fetch		:	*
	
[Collection:EAT_InquiryBatchSrc]
	Data Source	:	Variable		:	EAT_InquiryBatch
	
	Fetch		:	*
	
[Collection:EAT_InquiryInventory]
	Source Collection		:	EAT_InquiryBatchSrc
	
	By			:	StockItemName	:	$ItemName
	By			:	RateStr			:	$$String:$Rate
	
	Compute		:	Rate			:	$Rate
	
	Aggr Compute:	BilledQty		:	Sum		:	$Qty
	Aggr Compute:	Amount			:	Sum		:	$Amount
	
	Keep Source	:	().
	Client Only	:	Yes
	
[Collection:EAT_InquiryBatches]
	Use			:	EAT_InquiryInventory
	
	By			:	GodownName		:	$GodownName
	By			:	BatchName		:	$BatchName
	By			:	DueDateStr		:	$$String:$DueDate
	
	Compute		:	OrderDueDate	:	$DueDate
	
	Filter		:	EAT_IsSameItemWithRate
	
[Collection:EAT_InquiryLedgersSrc]
	Data Source	:	Variable		:	EAT_InquiryLedgers
	
	Fetch		:	*

[Collection:EAT_InquiryLedgers]
	Source Collection		:	EAT_InquiryLedgersSrc
	
	By			:	LedgerName		:	$LedgerName
	By			:	IsDeemedPositive:	$IsDeemed
	
	Aggr Compute:	Amount			:	Sum		:	$Amount
	
	Keep Source	:	().
	Client Only	:	Yes
	
[Collection:EAT_CreateVouchers]
	Object		:	EAT_LocPurchaseEnquiry
	Object		:	EAT_LocPurchaseOrder
	Object		:	EAT_LocReceiptNote
	Object		:	EAT_LocPurchase
	Object		:	EAT_ForPurchaseEnquiry
	Object		:	EAT_ForPurchaseOrder
	Object		:	EAT_ForReceiptNote
	Object		:	EAT_ForPurchase
	Object		:	EAT_JobOrder
	Object		:	EAT_DeliveryNote
	Object		:	EAT_Sales
	
[Collection:EAT_DelNotesSrc]
	Type		:	Vouchers:VoucherType
	Child Of	:	$$VchTypeDelNote
	Belongs To	:	Yes
	
	Fetch		:	*
	
[Collection:EAT_DelNotes]
	Source Collection	:	EAT_DelNotesSrc

	Keep Source	:	().
	Client Only	:	Yes
	
	By			:	VCHName			:	$VoucherTypeName
	
	Aggr Compute:	Prices			:	Last		:	$EAT_Prices
	Aggr Compute:	Delivery		:	Last		:	$EAT_Delivery
	Aggr Compute:	Validity		:	Last		:	$EAT_Validity
	Aggr Compute:	OrderedBy		:	Last		:	$EAT_OrderedBy
	
	Search Key	:	$VCHName
	