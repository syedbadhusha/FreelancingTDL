[#Form:SalesColor]
	Option		:	EAT_SalesPrint				:	@@EAT_IsSalesCustomPrint
	
[!Form:EAT_SalesPrint]
	Delete		:	Print
	Add			:	Print		:	EAT_SalesPrint, EAT_SalesPrint2
	
[Report:EAT_SalesPrint]
	Use			:	EAT_PRNCommon

	Local		:	Part		:	EAT_PRNCMNOpening		:	Option		:	EAT_SalesOpening
	Local		:	Part		:	EAT_CMNAmountSummary	:	Option		:	EAT_SalesAdditionalDetails
	
	Local		:	Line		:	EAT_ColumnTitle			:	Option		:	EAT_SalesTitle
	Local		:	Line		:	EAT_Column				:	Option		:	EAT_SalesInfo

	Local		:	Line		:	EAT_ColumnTitle			:	Option		:	EAT_AddTracking		:	@@EAT_IsNotEmptyDN
	Local		:	Line		:	EAT_Column				:	Option		:	EAT_AddTracking		:	@@EAT_IsNotEmptyOwnerDN
	
	Local		:	Line		:	EAT_ColumnTitle			:	Option		:	EAT_AddVATAmount	:	@@IsSales
	Local		:	Line		:	EAT_Column				:	Option		:	EAT_AddVATAmount	:	@@IsSales
	
[!Part:EAT_SalesOpening]
	Local		:	Part		:	EAT_VoucherDetails		:	Add			:	Line		:	EAT_VCHOrderNo, EAT_VCHOrderDate
	
[Line:EAT_VCHOrderNo]
	Field		:	MediumPrompt, NameField
	
	Local		:	Field		:	MediumPrompt			:	Set as		:	"P.O. No."
	Local		:	Field		:	NameField				:	Set as		:	$BasicPurchaseOrderNo;$EAT_LPONo

[Line:EAT_VCHOrderDate]
	Field		:	MediumPrompt, NameField
	
	Local		:	Field		:	MediumPrompt			:	Set as		:	"P.O. Date"
	Local		:	Field		:	NameField				:	Set as		:	$BasicOrderDate

[!Line:EAT_SalesFields]
	Add			:	Field		:	After	:	EAT_SNo		:	EAT_OrderNo, EAT_Name
	
[!Line:EAT_AddTracking]
	Add			:	Field		:	After	:	EAT_OrderNo	:	EAT_TrackingNo

[!Line:EAT_AddVATAmount]
	Add			:	Right Field	:	EAT_VATAmount, EAT_AmountIncl
		
[!Line:EAT_SalesTitle]
	Use			:	EAT_SalesFields
	
	Local		:	Field		:	EAT_Name				:	Set as		:	"Equipment"
	Local		:	Field		:	EAT_Description			:	Set as		:	"Job Description"
	Local		:	Field		:	EAT_TrackingNo			:	Set as		:	"DN No."
	
[!Line:EAT_SalesInfo]
	Use			:	EAT_SalesFields
	
	Local		:	Field		:	EAT_Description			:	Set as		:	$BasicUserDescription
	
[!Part:EAT_SalesAdditionalDetails]
	Replace		:	Part		:	EAT_CMNAmountInWords	:	EAT_SalesAmountInWords
		
[Part:EAT_SalesAmountInWords]
	Part		:	EAT_CMNAmountInWords, EAT_SalesAddDetails
	
	Vertical	:	Yes
	
[Part:EAT_SalesAddDetails]
	Space Top	:	2 mms
	
	Line		:	EAT_QuoPaymentTerms, EAT_SalesOrderBy, EAT_SalesReceiver, EAT_SalesReceiverName
	
	Local		:	Field		:	MediumPrompt			:	Width		:	30 mms
	Local		:	Field		:	NameField				:	Full Width	:	Yes
	
	Local		:	Field		:	MediumPrompt			:	Style		:	Normal
	
	Local		:	Field		:	ShortNameField			:	Border		:	ThinBottom
	
	Local		:	Line		:	EAT_QuoPaymentTerms		:	+
		Local	:	Field		:	MediumPrompt			:	Set as		:	"Pay Terms"
		
[Line:EAT_SalesOrderBy]
	Field		:	MediumPrompt, NameField
	
	Local		:	Field		:	MediumPrompt			:	Set as		:	"Ordered By"
	Local		:	Field		:	NameField				:	Set as		:	$EAT_OrderedBy
	
[Line:EAT_SalesReceiver]
	Field		:	MediumPrompt, ShortNameField
	
	Local		:	Field		:	MediumPrompt			:	Set as		:	"Receiver's Name"
	
[Line:EAT_SalesReceiverName]
	Field		:	MediumPrompt, ShortNameField
	
	Local		:	Field		:	MediumPrompt			:	Set as		:	"Receiver's Sign"
	
[Report:EAT_SalesPrint2]
	Use			:	EAT_PRNCommon

	Local		:	Part		:	EAT_PRNCMNOpening		:	Option		:	EAT_SalesOpening
	Local		:	Part		:	EAT_CMNAmountSummary	:	Option		:	EAT_SalesAdditionalDetails
	
	Local		:	Part		:	EAT_Column				:	Option		:	EAT_SalesPrint2
	
	Local		:	Line		:	EAT_ColumnTitle			:	Option		:	EAT_SalesTitle2
	Local		:	Line		:	EAT_Column				:	Option		:	EAT_SalesInfo2

[!Part:EAT_SalesPrint2]
	Delete		:	Part		:	EAT_LedgerEntries
	
	Local		:	Part		:	Default					:	Delete		:	Repeat
	
[!Line:EAT_SalesTitle2]
	Use			:	EAT_SalesFields2
	
	Local		:	Field		:	EAT_Name				:	Set as		:	"Equipment"
	Local		:	Field		:	EAT_Description			:	Set as		:	"Job Description"
	
[!Line:EAT_SalesInfo2]
	Use			:	EAT_SalesFields2
	
	Local		:	Field		:	EAT_OrderNo				:	Set as		:	If $$IsSysName:$BasicPurchaseOrderNo then "" else $BasicPurchaseOrderNo
	Local		:	Field		:	EAT_Name				:	Set as		:	$JC_VCHEquipment
	Local		:	Field		:	EAT_Description			:	Set as		:	$JC_VCHMemo
	Local		:	Field		:	EAT_Amount				:	Set as		:	$Amount - @@EAT_Discount - @@EAT_VATAmount - @@EAT_RoundOff
	
[!Line:EAT_SalesFields2]
	Add			:	Field		:	After	:	EAT_SNo		:	EAT_OrderNo, EAT_Name
	Delete		:	Right Field	:	EAT_Qty, EAT_RateSymbol, EAT_Rate

[Field:EAT_VATAmount]
	Use			:	AmountForexField
	Width		:	25 mms
	Set as		:	If $$IsEmpty:@transnature Then "" Else If ##vIsDebit Then $$AsDrAmt:@taxamt Else $$AsCrAmt:@taxamt
    taxamt		:	$$ReportObject:$$CollectionFieldByKey:$VTAGCCInv_TaxAmount:@@GVATTaxAnalysisItemLedKey:VATGCCTaxAnalysisAlongWithItems
    transnature	:	$$ReportObject:$$CollectionFieldByKey:$VATGCCInv_TransNature:@@GVATTaxAnalysisItemLedKey:VATGCCTaxAnalysisAlongWithItems
    
[Field:EAT_AmountIncl]
	Use			:	AmountForexField
	Width		:	25 mms
	Set as		:	$Amount + #EAT_VATAmount