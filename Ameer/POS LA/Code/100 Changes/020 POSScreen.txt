


[System: Events]
	
	SysDateVCHDate 	: Load Company  : Yes	: Call	: FuncSysDateVCHDate
	
[Function: FuncSysDateVCHDate]
	
	40 : SET : SVCurrentDate : $$MachineDate
	
[Report:LA_POS_Voucher]
		
	Use	:	Voucher
	Variable	:	LA_MultiPayAccept,LA_PayTypeStr 
	Variable	:	LA_POSMultiCardAmount,LA_POSMultiBenefitAmnt,LA_POSMultiCashAmnt
	Variable	:	LA_BillPaid	

	Local	:	Style	:	Default	:	Font	:	"Verdana"
	Local	:	Form	:	Default	:	Background	:	White

[Form:LA_POS_Voucher]

	Use	:	voucher
	SetAlways       		: SVClass           : "POS"

	Key		                : POS Print Key, POS Mail Key, POS Share Key,POS Export Key
	Delete	:	Button
	add	:	Button	:	POS_Print

;;;;;;;;;;;;;;

	Control     : LA_PaymethodSelect : Not ##LA_BillPaid and $$InCreateMode
	Print after Save	: if ##LA_PayTypeStr = "HOLD" then No else Yes
	No Confirm : Yes

	set	:	LA_MultiPayAccept	:	No
	Set	:	LA_BillPaid			:	No
	Set	:	LA_PayTypeStr		:	"CASH"
	
	set	:	CashTenderdAmountVar	:	""
	set	:	LA_POSMultiCashAmnt		:	""	
	set	:	LA_POSMultiBenefitAmnt	:	""

	set	:	LA_POSMultiCardType		:	""
	set	:	LA_POSMultiCardAmount	:	""
	set	:	LA_POSMultiCardNumber	:	""


;;;;;;;;Screen form

;
;	Set				:	VARVchDate		:	$$MachineDAte
;
;
	Option	:	LA_POS_CreateVch	:	@@VchPOSInvoice

	On	:	Form Accept		  :	yes					:	Call	 :	UpdatePaidMethod:##LA_PayTypeStr	
	

[!Form:LA_POS_CreateVch]

	Option	: Small Size Form
	
	Delete	:	Button	:	POS MultiPymtButton
	
	Width	: 100 % page
	Height	: 100 % Page		
	Delete	: Bottom ToolBar Buttons		
	

	Delete	:	Part	:	POSPartyInfo

	Local	:	Style	:	Default	:	Font	:	"Verdana"
	Background	:	White

	Local	:	Part	:	VCH Title2 Middle	:	Invisible	:	Yes

	Local	:	Part	:	VCH Title2			:	Invisible	:	Yes
	Local	:	Part	:	VCH Title			:	Invisible	:	Yes

	Add	:	Part	:	At Beginning	:	LA_POS_VCHTitle,LA_POS_VCHTop

	;;;;;;;;;;;;;  Colunm Title Changes 
	
	Local	:	Part	:	EIColumn :	Local	:	Line	:	EIColumnOne			:	Option	:	LA_EIColumnOne		:	Yes
	Local	:	Part	:	EIColumn :	Local	:	Line	: 	EIColumnOneRate		:	Option	:	LA_EIColumnOneRate	:	Yes
	Local	:	Part	:	EIColumn :	Local	:	Line	:	EIColumnTwo			:	Option	:	LA_EIColumnTwo		:	Yes

	Local	:	Part	:	EIColumn :	Local		:	Field		:	Default :Color       : @@SV_VCHCOLOR
	Local	:	Part	:	EIColumn :	Background  : 	@@SV_VCHTYPE

;;;;;;;;;;;;;;  Ei Details Column Changes	;;;;;;;;

	Local	:	Line	:	EI InvInfo	:	Option	:	LA_EIInvInfo	:	Yes
	Local	:	Line	:	CI InvInfo	:	Option	:	LA_EIInvInfo	:	Yes


	Local	:	Part	:	EIPOSDetails	:	Delete	:	Common Border
	
	Delete	:	Top Parts	:	EIPOSDetails
	Add	:	Top Parts	:	LA_EIPOSDetails
	
	Local	:	Part	:	VCHPOSModeOfReceipts	:	Invisible	:	Yes


	Delete	:	Bottom Part	:	VCH POS Narration
	Local	:	Part	:	Cust_VCHPOSModeOfReceipts	:	Border	:	Thin Top
	
	Local	: Part	:	VCHPOSModeOfReceipts	: Local		:	Field	:	VCHPOSBankLedger	:	Set as	:	$POSBankLedgerClass:VoucherType:##SVVoucherType
	Local	: Part	:	VCHPOSModeOfReceipts	: Local		:	Field	:	VCHPOSCardLedger	:	Set as	:	$POSCardLedgerClass:VoucherType:##SVVoucherType
	Local	: Part	:	VCHPOSModeOfReceipts	: Local		:	Field	:	VCHPOSCashLedger	:	Set as	:	$POSCashLedgerClass:VoucherType:##SVVoucherType


;;;;;;;;;;;;;;  Column Title Changes	;;;;;;


			[!Line:LA_EIColumnOne]

				Add	:	Right Fields	:	At End	:	LA_VCHDiscTitle,LA_VCHSubTaxTitle,LA_VCHSubTotalTitle, LA_InclVATTotalTitle

			[!Line:LA_EIColumnOneRate]

				Add	:	Right Fields	:	At End	:	LA_VCHDiscTitle,LA_VCHSubTaxTitle,LA_VCHSubTotalTitle, LA_InclVATTotalTitle
					
				Local	:	Field	:	LA_VCHDiscTitle				:	Set as	:	""
				Local	:	Field	:	LA_VCHSubTaxTitle			:	Set as	:	""
				Local	:	Field	:	LA_VCHSubTotalTitle			:	Set as	:	""
				Local	:	Field	:	LA_InclVATTotalTitle		:	Set as	:	""
				
			[!Line:LA_EIColumnTwo]	

				Add	:	Right Fields	:	At End	:	LA_VCHDiscTitle,LA_VCHSubTaxTitle,LA_VCHSubTotalTitle, LA_InclVATTotalTitle

				Local	:	Field	:	LA_VCHDiscTitle				:	Set as	:	""
				Local	:	Field	:	LA_VCHSubTaxTitle			:	Set as	:	""
				Local	:	Field	:	LA_VCHSubTotalTitle			:	Set as	:	""
				Local	:	Field	:	LA_InclVATTotalTitle		:	Set as	:	""


			;;;;;;;;;;;  Column Details Changes	;;;;;;;;

			[!Line:LA_EIInvInfo]
				
				Add	:	Right Fields	:	At End	:	LA_VCHDisc,LA_VCHSubTax,LA_VCHSubTotal, LA_VCHSubTotalIncl
				Local	:	Field	:	Default	:	Border	:	thin box
	
;;;;;;;;;;;;  Body Part Changes

[Part:LA_EIPOSDetails]
	
	Part	:	LA_EIPOSDetailsMn,LA_POSInvRight	
	Vertical	:	No


		[Part: LA_EIPOSDetailsMn]

			Use		: EI Details
			Delete	: Switch
			Add		: Switch	: EI POSClassDetail		: LA_EINrmlDetails    : (NOT @@POSINVClassFmt) OR $$InDuplicateMode
	        Add		: Switch	: EI POSClassDetail		: LA_EIClassDetails   : @@POSINVClassFmt
			add	:	Bottom Part	:	Cust_VCHPOSModeOfReceipts
			Delete	:	Common Border

	;;;;;;;;;;;;;;;;;;;; EI Part;;;;

        [!Part: LA_EINrmlDetails]

            Add : Part  : EIInvInfo,LA_EIInfo
			Local	:	Part	:	EIInvInfo	:	Scroll	:	Vertical
			Total   : VCH Value, EI Value

        [Part: LA_EIInfo]

            Parts   : LA_EIAccInfo
            Vertical: Yes
            Scroll  : Vertical
			Invisible	:	Yes

		[Part:LA_EIAccInfo]
			
			Part	:	EIAccInfo
						
;;;;;;;;;;;;;;;;;;  CI Part

        [!Part: LA_EIClassDetails]

            Add : Part  : CIInvInfo,LA_CIInfo
			Local	:	Part	:	CIInvInfo	:	Scroll	:	Vertical
			Total   : VCHValue, CIValue

			[Part: LA_CIInfo]

				Parts   : LA_CIAccInfoMN
				Vertical: Yes
				Scroll  : Vertical
				Invisible	:	Yes
					
				[Part:LA_CIAccInfoMN]					
					Part	:	CIAccInfo
					
	[Part:LA_POS_VCHTitle]
			Line		: LA_POS_VCHTitle
			[Line:LA_POS_VCHTitle]
				Field:LA_POS_VCHTitle
				[Field:LA_POS_VCHTitle]
					    Use         : Short Name Field
						Color       : @@SV_VCHCOLOR
						Background  : @@SV_VCHTYPE
						Skip        : Yes
						Set as      : ##SVVoucherType
						Set always  : Yes
						Modifies    : SVVoucherType
						Storage     : Voucher Type Name
						Align       : Center
						Style		: Large Bold
						Full Width	:	Yes

	[Part: LA_POS_VCHTop]

		Left Part		: LA_POS_VCHTopLeft
		Right Part		: LA_POS_VCHTitleRight
		Border			: Thin Bottom
		Vertical		: No
		Space Top		: 0.15
		Space Bottom	:	0.15
		Local	:	Field	:	Default	:	Color	:	Royal Blue

;;;;;;;;;;;;;;;;;  Warehouse and Customer ;;;;;;;;;;;;;;

		[Part:LA_POS_VCHTopLeft]
			
			Part	:	LA_POSGodownInfo,POSPartyInfo
			Vertical	:	Yes

			Local	:	Part	:	POS PartyNameInfo	:	Add	:	Line	:After:	VCHPOSPartyContact	:	LA_POSPaymentMethod
				
			Local	:	Field	:	VCH POS PartyContact	:	Delete	:	Table
			Local	:	Field	:	VCH POS PartyContact	:	Delete	:	Sub Form
			Local	:	Line	:	VCH POS PartyContact	:	Local	:	Field	:	Simple Prompt	:	Set as	:	"Customer"
			Local	:	Line	:	VCH POS PartyContact	:	Local	:	Field	:	Simple Prompt	:	Width		: @@VCHLeftPromptWidth % Page
			Local	:	Field	:	VCH POS PartyContact	:	Width	:	100 MMS
			Local	:	Field	:	VCH POS PartyContact	:	Border	:	thin Box
			
			Local	:	Part	:	EI Price Level		:	Invisible	:	Yes
			Local	:	Part	:	POS PartyNameInfo	:	Delete		:	Width
			
			
			[Line:LA_POSPaymentMethod]
				Field	:	Medium Prompt,LA_POSPaymentMethod
				Local	:	Field	:	Medium Prompt	:	Set as	:	"Payment Method"
				Invisible	:	Not $$InDeveloperMode			
				
				[Field:LA_POSPaymentMethod]
					Use	:	Name Field
					Set Always	:	Yes
					Storage	:	LA_POSPaymentMethod
					
            [Part:LA_POSGodownInfo]

		        Part		: LA_POSGodown
				Invisible	: (NOT ($$IsMultiGodownOn OR @@IsSingaporeGSTEnabled)) 
				
				[Part: LA_POSGodown]
            
					Line	: LA_VCHPOSGodown								
					Local   : Line	: LA_VCHPOSGodown	: Local: Field: Default			: Inactive	: NOT $$IsMultiGodownOn 
					Local   : Line	: LA_VCHPOSGodown	: Local: Field: Short Prompt	: Width		: @@VCHLeftPromptWidth % Page

					 [Line: LA_VCHPOSGodown]

						Field                   : Short Prompt, LA_VCHPOSGodown

						Local                   : Field : Short Prompt  : Set as	:	"Warehouse"
						Invisible               : NOT $$IsMultiGodownOn
							
						[Field: LA_VCHPOSGodown]
							 
							Use             : Name Field
							Use				: Create Godown
							Set as          : If $$InAlterMode OR $$InDuplicateMode Then $$CollectionField:$GodownName:First:InventoryEntries Else ##POSGodown
							Table           : Godown, Create Master
							Keys            : Create Godown, Alter Godown
							Modifies        : POSGodown	: Yes
							Variable        : SV Godown
							Background		: white
							Border			: Thin Box
							Width	:	100 MMS
							
;;;;;;;;;;;;;;;;;;;;;;   Date and VChNo	;;;;;;;;;;;;;;;;;
							
		[Part:LA_POS_VCHTitleRight]
			Part	:LA_POS_VCHDate,LA_POS_VCHNo
			Vertical	:	Yes
			Space Top	:	0.15

			[Part:LA_POS_VCHDate]
				Line	:	VCH Date
;				Local	:	Field	:	Plain VCH Date	:	Set as	:	$$MachineDate
				Local	:	Line	:	VCH Date			:	local	:	Field:VCH Date:Add	:	Field	:	Before	:Plain VCH Date:MediumPrompt
				Local	:	Field	:	MediumPrompt		:	Set as	:	"Date"
				Local	:	Field	:	MediumPrompt		:	Width	:	30 MMS
				Local	:	Line	:	VCH Date			:	local	:	Field:VCH Date:Local:	Field	:	Plain VCH Date:Width	:	70 MMS
				
			[Part:LA_POS_VCHNo]

				Line	:	LA_POS_VCHNo
				Space Top	:	0.15
				
				[Line:LA_POS_VCHNo]

					Right Field	:	MediumPrompt, VCH Number, VchNumber DispSeries
					
					Local	:	Field	:	MediumPrompt	:	Set as	:	if Not @@IsCreditNote then "POS Number" else "POS Return No"
					Local	:	Field	:	MediumPrompt	:	Width	:	30 MMS
					Local	:	Field	:	VCH Number		:	Border	:	Thin Box
					Local	:	Field	:	VCH Number		:	Width	:	70 MMS

;;;;;;;;;;;;;;;;;;;;  POS Main Body	;;;;;;;;;;;

					[Field:LA_VCHDiscTitle]
						Width       : @@VCHQtyWidth
						Setas       : $$LocaleString:"Discount"
						Style       : Small Bold
						Align       : Centre
						Skip        : Yes
						Fixed       : Yes

					[Field:LA_VCHSubTaxTitle]
						Width       : @@VCHQtyWidth
						Setas       : $$LocaleString:"Tax"
						Style       : Small Bold
						Align       : Centre
						Skip        : Yes
						Fixed       : Yes
						Invisible	:	Yes
						
					[Field:LA_VCHSubTotalTitle]
						Width       : @@VCHQtyWidth
						Setas       : $$LocaleString:"Sub Total"
						Style       : Small Bold
						Align       : Centre
						Skip        : Yes
						Fixed       : Yes
						
					[Field:LA_InclVATTotalTitle]
						Width       : @@VCHQtyWidth
						Setas       : $$LocaleString:"Total Incl"
						Style       : Small Bold
						Align       : Centre
						Skip        : Yes
						Fixed       : Yes
						Invisible	: @@InclTaxcfg
						
							
				[Field:LA_VCHDisc]

					Use			: Amount Forex Field
					Style       : Small
					Align       : Centre
					Border      : Thin Right
					Storage		: LA_VCHDisc
					Set Always	: Yes
					Width       : @@VCHQtyWidth	
					Inactive    : $$IsEnd:$StockItemName OR @@NoBaseUnits

				[Field:LA_VCHSubTax]

					Use         : Amount Forex Field
					Style       : Small
					Align       : Centre
					Border      : Thin Right
					Storage		: LA_VCHSubTax
					Skip		: Yes
					Set Always	: Yes
					Width       : @@VCHQtyWidth
					Set as      : If $$IsEmpty:@transnature Then "" Else If ##vIsDebit Then $$AsDrAmt:@taxamt Else $$AsCrAmt:@taxamt
					taxamt      : $$ReportObject:$$CollectionFieldByKey:$VTAGCCInv_TaxAmount:@@GVATTaxAnalysisItemLedKey:VATGCCTaxAnalysisAlongWithItems
					transnature : $$ReportObject:$$CollectionFieldByKey:$VATGCCInv_TransNature:@@GVATTaxAnalysisItemLedKey:VATGCCTaxAnalysisAlongWithItems
					Inactive    : $$IsEnd:$StockItemName OR @@NoBaseUnits
					Read Only	: Yes
					Invisible	:	Yes

				[Field:LA_VCHSubTotal]
					
					Use			: Amount Forex Field
					Style       : Small
					Align       : Centre
					Border      : Thin Right
					Skip        : Yes
					Fixed       : Yes
					Storage		: LA_VCHSubTotal
					Width       : @@VCHQtyWidth
					Inactive    : $$IsEnd:$StockItemName OR @@NoBaseUnits
					Read Only	: Yes
					Set Always	: yes
					Set as		: $Amount - $LA_VCHDisc
				
				[Field:LA_VCHSubTotalIncl]
					Use			: Amount Forex Field
					Style       : Small
					Align       : Centre
					Border      : Thin Right
					Skip        : Yes
					Fixed       : Yes
					Width       : @@VCHQtyWidth
					Inactive    : $$IsEnd:$StockItemName OR @@NoBaseUnits
					Set Always	: yes
					Set as		: ($BilledQty * $InclusiveTaxValue)- $LA_VCHDisc
					Invisible	: @@InclTaxcfg

[Part:Cust_VCHPOSModeOfReceipts]	

	Part	:	Cust_VCHTotalDiscAndRnd,LA_POSInvDetailsLed
	Vertical	:	No
	Border	:	Thin Top
	[Part:Cust_VCHTotalDiscAndRnd]
		Line	:	LA_POSInvTotalTitle,LA_POSInvTotal
		
		[Line:LA_POSInvTotalTitle]
			
				Field		:	LA_POSItemDiscTotal,LA_Expense1, LA_Expense2, LA_POSInvDisc,LA_POSDiscTotal,+
								LA_POSRoundOff
							
				Local	:	Field	:	Default	:	Type:	String
				Local	:	Field	:	Default	:	Delete	:	Storage
				Local	:	Field	:	Default	:	Align	:	Center
				Local	:	Field	:	Default	:	Style	:	Small Bold
				Local	:	Field	:	Default	:	Skip	:	yes
				
				Local	:	Field	:	LA_Expense1				:	Set as	:	@@POS_AddLedger1Name
				Local	:	Field	:	LA_Expense2				:	Set as	:	@@POS_AddLedger2Name
				Local	:	Field	:	LA_POSItemDiscTotal		:	Set as	:	"Item Disc"+$$NewLine+"Total"
				Local	:	Field	:	LA_POSInvDisc			:	Set as	:	"Inv." +$$NewLine+"Discount"
				Local	:	Field	:	LA_POSDiscTotal			:	Set as	:	"Total"++$$NewLine+"Discount"
				Local	:	Field	:	LA_POSRoundOff			:	Set as	:	"Round Off"
				
				Local	:	Field	:	Default					:	Line	:	2
				
		[Line:LA_POSInvTotal]

				Field	: LA_POSItemDiscTotal,LA_Expense1, LA_Expense2,LA_POSInvDisc,LA_POSDiscTotal,+
							LA_POSRoundOff
				Local	:	Field	:	Default	:	Border	:	Thin Box
				Space Top	:	3 MMS
				

;;;;;;;;;;;;;;  Vat And Total	
	
[Part:LA_POSInvDetailsLed]

	Part	:	LA_POSInvTaxableAmount,LA_POSInvLedLeft,LA_POSInvGrandTotal
	Horizontal Align	:	Right
	Vertical	:	No
	Local	:	Field	:	Default	:	Border	:	Thin Box

	[Part:LA_POSInvTaxableAmount]

		Line:LA_POSInvTaxableAmountTit,LA_POSInvTaxableAmount
					
		[Line:LA_POSInvTaxableAmountTit]
			
			Field	:	LA_POSInvTotal
			Local	:	Field	:	LA_POSInvTotal	:	Set as	:	"Taxable" +$$NewLine+"Amount"			
			Local	:	Field	:	LA_POSInvTotal	:	Align	:	Center
			Local	:	Field	:	LA_POSInvTotal	:	Type	:	String
			Local	:	Field	:	LA_POSInvTotal	:	Line	:	2
			Local	:	Field	:	LA_POSInvTotal	:	Skip	:	Yes
			Local	:	Field	:	LA_POSInvTotal	:	Style	:	Large Bold
			
							
		[Line:LA_POSInvTaxableAmount]
						
			Field	:	LA_POSInvTaxableAmount

			Local	:	Field	:	LA_POSInvTaxableAmount			:	Border		:	Thin Box
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Background	:	Dark Red
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Color		:	white
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Read Only	:	Yes
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Width		:	35 MMS
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Style		:	Large Bold
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Set as		:	if @@isCreditNote then #EIValueTotal-#LA_POSInvDetailsLed else #VCHPOSInvoiceTotal-#LA_POSInvDetailsLed

	[Part:LA_POSInvLedLeft]
		
		Line:LA_POSInvDetailsLedTit,LA_POSInvDetailsLed
;		Repeat:LA_POSInvDetailsLed	:	LedgerEntries
					
		[Line:LA_POSInvDetailsLedTit]
			
			Field	:	LA_POSInvTotal
			Local	:	Field	:	LA_POSInvTotal	:	Set as	:	"VAT" +$$NewLine+"Amount"			
			Local	:	Field	:	LA_POSInvTotal	:	Align	:	Center
			Local	:	Field	:	LA_POSInvTotal	:	Type	:	String
			Local	:	Field	:	LA_POSInvTotal	:	Line	:	2
			Local	:	Field	:	LA_POSInvTotal	:	Skip	:	Yes
			Local	:	Field	:	LA_POSInvTotal	:	Style	:	Large Bold
							
		[Line:LA_POSInvDetailsLed]
						
			Field	:	LA_POSInvDetailsLed

			Local	:	Field	:	LA_POSInvDetailsLed			:	Border	:	Thin Box
			Local	:	Field	:	LA_POSInvDetailsLed			:	Background	:	Dark Red
			Local	:	Field	:	LA_POSInvDetailsLed			:	Color	:	white
			Local	:	Field	:	LA_POSInvDetailsLed			:	Read Only	:	Yes
			Local	:	Field	:	LA_POSInvDetailsLed			:	Width	:	35 MMS
			Local	:	Field	:	LA_POSInvDetailsLed			:	Style	:	Large Bold	
			

			[Field:LA_POSInvDetailsLed]

				Use         : Amount Forex Field
				Delete	:	Border
				Style	:	Large Bold
				Read Only	:	Yes
				Width	:	35 MMS
				Color	:	white
				Background	:	Dark Red
				Set Always	:	yes
				Set as	:	$$LA_VATAmount
				
[Function:LA_VATAmount]

	Variable	:	LA_VATAmountVar	:	Amount

	100	:	Walk Collection	:	LedgerEntries
	200	:		If	:	$$IsSysNameEqual:VAT:$TaxType:Ledger:$LedgerName
	300	:			Set	:	LA_VATAmountVar	:	$Amount
	400	:		End If
	500	:	End Walk
	
	600	:	Return	:	##LA_VATAmountVar

[Part:LA_POSInvGrandTotal]
	Line	:	LA_POSInvGrandTotalTit,LA_POSInvGrandTotal
	
	[Line:LA_POSInvGrandTotalTit]
		
		Field	:	LA_POSInvTotal
		Local	:	Field	:	LA_POSInvTotal	:	Set as	:	"Grand" +$$NewLine+"Total"
		Local	:	Field	:	LA_POSInvTotal	:	Skip	:	Yes
		Local	:	Field	:	LA_POSInvTotal	:	Align	:	Center
		Local	:	Field	:	LA_POSInvTotal	:	Type	:	String
		Local	:	Field	:	LA_POSInvTotal	:	Line	:	2
		Local	:	Field	:	LA_POSInvTotal	:	Style	:	Large Bold
		
		[Line:LA_POSInvGrandTotal]
						
			Field	:	VCHPOSInvoiceTotal
			
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Style	:	Large Bold					
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Read Only	:	Yes
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Width	:	35 MMS
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Color	:	white
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Background	:	Dark Red
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Skip	:	No

		;;;;;;;;;;;;;;;;;;;  Invoice Total Fields	;;;;;;;;;

			
		[Field:LA_Expense1]
			Invisible	:	$$IsSysName:@@POS_AddLedger1Name
			Inactive	:	$$IsSysName:@@POS_AddLedger1Name
			Use			:	Amount Forex Field
			Set Always	:	Yes
			Width		:	25 MMS
			Set as		:	$LedgerEntries[1, @@POS_IsAddLedger1].Amount
			On		:	Accept	:	Yes	:	Call	:	PS_UpdateAddLedger:@@Pos_addledger1name:$$value
			on		:	accept	:	yes	:	field accept
	
	
		[Field:LA_Expense2]
			
			Invisible	:	$$IsSysName:@@POS_AddLedger2Name
			Inactive	:	$$IsSysName:@@POS_AddLedger2Name
			Use			:	Amount Forex Field
			Set Always	:	Yes
			Width		:	25 MMS
			Set as		:	$LedgerEntries[1, @@POS_IsAddLedger2].Amount
			On		:	Accept	:	Yes	:	Call	:	PS_UpdateAddLedger:@@POS_AddLedger2Name:$$Value
			On		:	Accept	:	Yes	:	Field Accept

		[Field:LA_POSItemDiscTotal]

			Use			:	Amount Forex Field
			Set Always	:	Yes
			Set as		:	$$CollAmtTotal:InventoryEntries:$$Abs:$LA_VCHDisc
			Skip		:	Yes
			Width		:	25 MMS
			Read Only	:	Yes
			
		[Field:LA_POSInvDisc]
			Use	:	Amount Forex Field
			Set Always	:	Yes
			Width		:	25 MMS
			Format		:	"No Zero"
			Storage		:	LA_POSInvDisc
			Style		:	Normal Bold
			Align		:	Right

		[Field:LA_POSDiscTotal]

			Use	:	Amount Forex Field
			Set Always	:	Yes
			Width		:	25 MMS
			Storage     : 	LA_POSDiscTotal
			Set as		:	(#LA_POSItemDiscTotal + #LA_POSInvDisc)
			Style		:	Normal Bold
			Read Only	:	Yes
			
			On	:	Accept	:	Yes	:	Call	:	UpdateLedEntriss:$$Value:#LA_POSRoundOff
			On	:   Accept	:	Yes	:	Field Accept

		[Field:LA_POSRoundOff]
			Use	:	Amount Forex Field
			Set Always	:	Yes
			Width		:	25 MMS
			Style		:	Normal Bold
			Storage		:	LA_POSRoundOff
			
			On	:	Accept	:	Yes	:	Call	:	UpdateLedEntriss:#LA_POSDiscTotal:$$Value
			On	:   Accept	:	Yes	:	Field Accept
			
		[Field:LA_POSInvTaxableAmount]

			Use         : Amount Forex Field
			Set as      : ""
			Style		:	Normal Bold
			Set Always	:	yes	
			
			
		[Field:LA_POSInvTotal]
			Use         : Amount Forex Field
			Delete	:	Border
			Style	:	Large Bold
			Read Only	:	Yes
			Width	:	35 MMS
			Color	:	white
			Background	:	Dark Red
			Set Always	:	yes
			
	;;;;;;;;;;;; Number Pad	;;;;;;;;			
	[Part:LA_POSInvRight]
		Part	: LA_POSSettlements,LA_POSNum
		Vertical	:	Yes
		Border	:	Thin Left
		Common Border	:	Yes
		[Part:LA_POSSettlements]
			
			Part	:	LA_POSCashAndBenefit,LA_POSMultiMode,+
						LA_POSHoldAndRetake,LA_POSReturn
			Vertical: Yes
			Background: Pattens Blue
			Height	:	34% page
			Width	: 24 % Page
			Border	: Thin Box
			Horizontal Align: Center


			[Part:LA_POSCashAndBenefit]

				Button	:	LA_POSCash,LA_POSBenefit

			[Part:LA_POSMultiMode]

				Button	:	LA_POSMultiPay

			[Part:LA_POSHoldAndRetake]

				Button:LA_POSHold,LA_POSReTake

			[Part:LA_POSReturn]

				Button	:LA_POSReturn

		[Part:LA_POSNum]
			Part	: LA_POS_Num7to9, LA_POS_Num4to6,+
					  LA_POS_Num3to1, LA_POS_Num0AndPoint,+
					  LA_POS_NumEnt,POS_Back
					  
			Vertical: Yes
			Background: Pattens Blue
			Height	: 50 % Page
			Width	: 24 % Page
			Border	: Thin Box
			Horizontal Align: Center

		[Button: NumberBadDesign]
			
			Horizontal Align: Center
			Height	: 8 % Page
			Width	: 8 % Page
			Skip Forward: Yes
			Key Style: NumberKeyStyle
			Style	 : NumberKeyStyle
			Key Color: Black 

	;;;;;;;;  Button Parts

			[Part: LA_POS_Num7to9]
		
				Button: POS_NUM7, POS_NUM8, POS_NUM9

			[Part: LA_POS_Num4to6]
		
				Button: POS_NUM6, POS_NUM5, POS_NUM4

			[Part: LA_POS_Num3to1]
		
				Button: POS_NUM3, POS_NUM2, POS_NUM1

			[Part: LA_POS_Num0andPoint]
				
				Button: POS_NUM0,POS_NUMPoint,POS_NUMBS
				

			[Part: LA_POS_NumEnt]
				
				Button: LA_POS_NumEnt
		
			[Part:POS_Back]
				Button	:	POS_BackInReport
				Local	:	Button	:	POS_BackInReport	:	Background	:	Dark Red
				Local	:	Button	:	POS_BackInReport	:	Width	: 23.5 % Page
				Local	:	Button	:	POS_BackInReport	:	Color	:	White
				Local	:	Button	:	POS_BackInReport	:	key Color	:	White
				Local	:	Button	:	POS_BackInReport	:	Horizontal Align	:	Center
				Local	:	Button	:	POS_BackInReport	:	Vertical Align		:	Center



;;;;;;;;;;;;;;;;;   Change Chequw as Benefit pay ;;;;;;


[#Line: POS ChequeLedger]
	
	Local		: Field	: Short Prompt		: Set as	: $$LocaleString:"Benefit Pay" + ":"


[#Field: VCHPOSRate]
	
	Option	:	POS_VCHPOSRate	:	@@POS_IsEnabled
	
[!Field:POS_VCHPOSRate]
	
	Delete	:	Set as	

	Add		:	Set as		: 	  If (@TaxationEnabled AND (@@InvoiceInCreate OR NOT $$IsEmpty:$InclusiveTaxValue) AND ##ICFGInclTax AND @@SalesInvoiceMode) AND NOT ($$IsEmpty:$$Number:$InclusiveTaxValue OR (($Amount != 0) AND ($Amount != @POSCalcedAmt))) Then $$POS_InclusiveTaxValue:@@ExItemRate:@@STItemRate:@@VATItemRate:@@TCSItemRate:@@GSTItemRate:@@GSTItemQtyRate:@@ExItemMRPDutyValue:@@VATItemMRPDutyValue:@@ExItemQuantum Else  +
								  If $StockItemName != #VCHPrevItem AND NOT $$IsStartupValue Then $$ForexValue:@@StdVchRate Else If $$InAlterMode OR $$InDuplicateMode Then @AltermodeVal Else +
								  If $$IsStartUpValue OR ($$IsEmpty:$Rate AND $$IsFieldEdited) Then $$Value Else If $$InCreateMode Then @POSCreateRate Else @AlterRate



[System:Formula]
	
		POS_VATItemRate     : If (@@IsRBVATSupportedState AND @@VATGetVatApplStatusofItem) OR @@IsGCCCountriesVATOn Then $$POS_GetVATItemRate Else 0


[Function: POS_InclusiveTaxValue]
			
	Parameter	: ExciseRate	: Number
	Parameter	: ST Rate		: Number
	Parameter	: VAT Rate		: Number
	Parameter	: TCS Rate		: Number
	Parameter	: GST Rate		: Number
	Parameter	: GSTQtyRate	: Number	: $$InitValue:Number
	Parameter	: ExciseMRPRate	: Amount	: $$InitValue:Amount
	Parameter	: VATMRPRate	: Amount	: $$InitValue:Amount
	Parameter	: ExciseQtmValue: Amount	: $$InitValue:Amount

	Variable	: InclTaxValue	: Rate		: $InclusiveTaxValue
	Variable	: StockItemRate	: Rate		: $InclusiveTaxValue
	Variable	: IsGSTStockItem: Logical	: If @@IsIndia AND @@VCHIsGSTRegularCompOnAndAppl Then $IsGSTApplStkItm Else No
	
;	Variable	: VATRateTempValue	: Amount

	Variable	: ExciseRateValue	: Number
	Variable	: STRateValue		: Number
	Variable	: VATRateValue		: Number
	Variable	: TCSRateValue		: Number
	Variable	: GSTRateValue		: Number
	
	Returns		: Rate
	
	001	: If		: ##IsGSTStockItem
	002	: 	Set		: GSTRateValue	: ##GSTRate/100
	003	: 	Set		: StockItemRate	: (##InclTaxValue-##GSTQtyRate) / (1+##GSTRateValue)
	004	: 	Do If	: ($$InCreateMode OR (NOT ##IsRateupdated))	: Call	: VCHBatchIsFieldAltered	: Yes
	005	: Return	: If $$Number:##StockItemRate <= 0 Then 0 Else ##StockItemRate
	
	006	: End If
	
	070	: Do If		: (NOT $$IsEmpty:##ExciseMRPRate) 	: Set	: ExciseRate	: $$Number:0
	075	: Do If		: (NOT $$IsEmpty:##ExciseMRPRate) 	: Set	: ExciseQtmValue: $$AsAmount:0
	085	: Do If		: (NOT $$IsEmpty:##VATMRPRate) 		: Set	: VAT Rate		: $$Number:0
	
	500	: Do If		: NOT ($$IsEmpty:##ExciseQtmValue) 	: Set	: InclTaxValue	: ##InclTaxValue - ##ExciseQtmValue * (1+ ##VATRate/100 + (1+##VATRate/100)* ##TCSRate/100)
	510	: Do If		: NOT ($$IsEmpty:##ExciseMRPRate) 	: Set	: InclTaxValue	: ##InclTaxValue - ##ExciseMRPRate * (1+ ##VATRate/100 + (1+##VATRate/100)* ##TCSRate/100)
	520	: Do If		: NOT ($$IsEmpty:##VATMRPRate) 		: Set	: InclTaxValue	: ##InclTaxValue - ##VATMRPRate * (1+ ##VATRate/100 + (1+##VATRate/100)* ##TCSRate/100)	

	010	: SET		: ExciseRateValue					: If $$IsEmpty:##ExciseRate	Then 0 Else ##ExciseRate/100
	020	: SET		: STRateValue						: If $$IsEmpty:##STRate	Then 0 Else ##STRate/100

	030	: SET		: VATRateValue						:  IF @@IsGCCCountriesVATOn AND ##VARVchDate >= ##GVATApplicableDate +		;;;;;;;;;Changed Tax Rate as 10 % instead of 5%  By Syed
																AND $$IsSysNameEqual:Taxable:@@GVATItemTotalTaxRate_Stk AND $$IsEmpty:##VATRate +
																	Then ((1+##ExciseRateValue+##STRateValue)*$$Number:10/100) Else If $$IsEmpty:##VATRate Then 0 +
																		Else (1+##ExciseRateValue+##STRateValue)*##VATRate/100

	040	: SET		: TCSRateValue						: If $$IsEmpty:##TCSRate Then 0 Else (1+##ExciseRateValue+##STRateValue+##VATRateValue) * ##TCSRate/100
				
	050	: Set		: StockItemRate						: ##InclTaxValue /(1+ ##ExciseRateValue + ##STRateValue + ##VATRateValue + ##TCSRateValue)
	
	055	: Call		: VCHBatchIsFieldAltered			: Yes
	
	056	: Set		: IsRateupdated	: False

	060	: Return	: If $$Number:##StockItemRate <= 0 Then 0 Else ##StockItemRate
