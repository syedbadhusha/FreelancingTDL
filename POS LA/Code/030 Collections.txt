<DefType:Collection>


[Collection: LA_POS_CompanyVouchers]

    Type        : Voucher
    Child of    : ##SVCurrentDate
    Report      : LA_POS_Voucher
	IsODBCTable : Yes

[Collection:POS_CompanyCreditNotes]
	Type		:	Voucher
	Child Of	:	##SVCurrentDate
	IsODBCTable	:	Yes
	Report		:	POS_CreditNoteEntry
	
[Collection:POS_CreditCardLedgers]
	Title		:	"Bank Account Ledgers"
	Use			:	AliasCollection
	Type		:	Ledger
	Include		:	Group:$$GroupBank
	
[Collection:POS_LedgerName]
	Type		:	Ledgers
	
	Search Key	:	$MasterId
	
	Keep Source	:	().
	Client Only	:	Yes
	
[Collection:POS_StockQuery]
	Use			:	StockQuery
	Delete		:	Report
	Add			:	Report		:	POS_StockQuery
	Delete		:	Trigger
	Add			:	Trigger		:	POS_StockItemName
	
[Collection:POS_Sales]
	Type		:	Vouchers:VoucherType
	Child Of	:	$$VchTypeSales
	Belongs To	:	Yes
	
	Fetch		:	*, Amount
	
	Compute		:	VATAmount				:	$$FilterAmtTotal:AllLedgerEntries:InvoiceVATLedger:$Amount
	Compute		:	POSRegCard				:	$$AsAmount:$$FilterValue:$Amount:AllLedgerEntries:1:POSCardFilter
	Compute		:	POSRegCheque			:	$$AsAmount:$$FilterValue:$Amount:AllLedgerEntries:Last:POSChqFilter
	Compute		:	POSRegCash				:	$$AsAmount:$$FilterValue:$Amount:AllLedgerEntries:1:POSCashFilter
	
	Filter		:	UseForPOSInvoice
	
[Collection:POS_SalesInventory]
	Source Collection			:	POS_Sales
	
	Walk		:	InventoryEntries
	
	By			:	StockItemName			:	$StockItemName
	
	Aggr Compute:	TotalQty				:	Sum			:	$BilledQty
	Aggr Compute:	TotalAmount				:	Sum			:	$Amount
	
	Sort		:	@@Default				:	$TotalQty
	
[Collection:POS_DayWiseSalesSrc]
	Type		:	Vouchers
	
	Fetch		:	*, Amount, AllLedgerEntries.*
	
	Compute		:	VATAmount				:	$$FilterAmtTotal:AllLedgerEntries:InvoiceVATLedger:$Amount
	Compute		:	POSRegCard				:	$$AsAmount:$$FilterValue:$Amount:AllLedgerEntries:1:POSCardFilter
	Compute		:	POSRegCheque			:	$$AsAmount:$$FilterValue:$Amount:AllLedgerEntries:Last:POSChqFilter
	Compute		:	POSRegCash				:	$$AsAmount:$$FilterValue:$Amount:AllLedgerEntries:1:POSCashFilter
	
	Filter		:	POS_IsSalesVouchers
	
[Collection:POS_DayWiseSales]
	Source Collection		:	POS_DayWiseSalesSrc
	
	By			:	DateFrom				:	$$String:$Date
		
	Aggr Compute:	TotalVAT				:	Sum			:	$VATAmount
	Aggr Compute:	TotalCard				:	Sum			:	If $$IsEmpty:$POSRegCard then 0 else $POSRegCard
	Aggr Compute:	TotalCash				:	Sum			:	If $$IsEmpty:$POSRegCash then 0 else $POSRegCash
	Aggr Compute:	TotalCheque				:	Sum			:	If $$IsEmpty:$POSRegCheque then 0 else $POSRegCheque
	
	Search Key	:	$DateFrom

[Collection:POS_STKPartNos]
	Title		:	"List Of Barcodes"
	
	Source Collection	:	StockItem
	
	Walk		:	PartNo
	
	Fetch		:	Name, PartNo, ClosingBalance, LastSalePrice
	
	Sub Title	:	"Part No", "Name", "Closing Balance"
	
	Format		:	$PartNo, 15
	Format		:	$Name, 35	
	Format		:	$ClosingBalance, 12
	
	Align       :	Right
    FullHeight  :	Yes
    With Alias  :	No

;;;;;;;;;;;;;;;;  For Retake
    
[Collection:POS_SalesRetake]
	Type		:	Vouchers:VoucherType
	Child Of	:	$$VchTypeSales
	Belongs To	:	Yes
	Title	:	"List Of Holded Bill"
	Sub Title	:	"Customer Name","Vch Number","Date","Amount","Id"
	Format	:	$PartyName
	Format	:	$VoucherNumber
	Format	:	$Date
	Format	:	$LA_POS_InvTotal
	Format	:	$MasterId
	Fetch		:	*, Amount,PartyLedgerName,VoucherNumber,MasterID,PartyName,LA_POS_InvTotal
	Align	:	Right
	Full Height	:	Yes
	Filter		:	UseForPOSInvoice,Isoptional
	

[Collection:POS_SalesReturn]
	Title		:	"List of Bills"
	
	Use			:	POS_SalesRetake
	Fetch		:	*, Amount,PartyLedgerName,VoucherNumber,MasterID,PartyName,LA_POS_InvTotal
	
	Delete		:	Filter
	Add			:	Filter		:	UseForPOSInvoice
	
[Collection:POS_BarcodeDetailsSrc]
	Data Source	:	Variable	:	POS_BarcodeDetails
	
[Collection:POS_BarcodeDetails]
	Source Collection			:	POS_BarcodeDetailsSrc
	
	Fetch		:	*
	
	Keep Source	:	().
	Client Only	:	Yes
	