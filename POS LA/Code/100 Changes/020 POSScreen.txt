


[System: Events]
	
	SysDateVCHDate 	: Load Company  : Yes	: Call	: FuncSysDateVCHDate
	
[Function: FuncSysDateVCHDate]
	
	40 : SET : SVCurrentDate : $$MachineDate
	
[Report:LA_POS_Voucher]
		
	Use	:	Voucher
	Variable	:	LA_MultiPayAccept,LA_PayTypeStr 
	Variable	:	LA_POSMultiCardAmount,LA_POSMultiBenefitAmnt,LA_POSMultiCashAmnt
	Variable	:	LA_BillPaid	

[Form:LA_POS_Voucher]

	SetAlways       		: SVClass           : "POS"
    Control     			: DateBelowBooksFrom: $Date < $BooksFrom:Company:##SVCurrentCompany
    Control     			: DateBelowFromDate : $Date < $$SystemPeriodFrom
    Control     			: DateBeyondToDate  : $Date > $$SystemPeriodTo
		
    Control         		: STXNonServicePartyExistsMsg   : (@@IsIndianServiceTax AND @@IsSTTaxenabledDate_Vch) AND @@IsJournal AND $$IsEmpty:$TaxAdjustment AND $$IsSTXJrnlTaxObjValidate
	Control					: BankAmountMisMatch			: (@@IsEbankingAlterReconTrans OR @@IsEbankingAlterExportedTrans) AND (IF ##SVSingleEntry Then #ACLSTotal != ##TotalBankAmount Else #VchDrAmtTotal != ##TotalBankAmount) AND $$InAlterMode
	Control					: EBankingAllocationsError		: $$InDuplicateMode AND $$BankAllocationError	;; If the amount decreased from the Ledger amount is greater than or equal to the LAst Bank Alloc Amount
		
    Horizontal Align		: Left
    Vertical Align  		: Top
    Space Bottom    		: 0
		
	Bottom Buttons			: Voucher Configuration
	Key		                : POS Print Key, POS Mail Key, POS Share Key,POS Export Key
	Button	:	POS_Print
    ;; Variable has been set on pressing Ctrl+A along with Form accept. This is to identify whether form is getting accepted using Ctrl + A
    ;; When form is accepted using Ctrl+A, in supplementary details form regeneration is not required. To stop the regeneration this approach is used.
    Key                     : FormAcceptKey

	On 	: Form Accept 		: (@@IsSales OR @@IsPurchase OR @@IsCreditNote OR @@IsDebitNote OR @@IsExciseGoodsTransfer OR @@IsJournal) 							: Call : VCHResetFormType
	On	: Form Accept   	: @@IsVATVchTaxDiffIgnored 									: Call  : VATVchInfoResetTaxDiff
	
	Set Always              : IsItemExists  : If $$InAlterMode Then NOT ($$IsEmpty:$AllInventoryEntries[1].StockItemName) Else No                                                      
    Set Always              : VoucherMode   : @@VchModeName
	Set						: IsGVATVoucher 											: Yes
	Bottom Toolbar Buttons 	: BottomToolBarBtn1, BottomToolBarBtn2_Accept, BottomToolBarBtn3_Delete, BottomToolBarBtn4_Cancel
	On	: Form Accept		: (@@IsGCCCountriesVATOn AND NOT @@IsOmanCountry AND @@IsPayment AND ($$IsSysNameEqual:VAT:$StatPaymentType) AND NOT $$IsEmpty:$TaxPayPeriodFromDate AND NOT $$IsEmpty:$TaxPayPeriodToDate)	: CALL : GVATVchUpdateSummaryAllocs	:@@GVATReturnPymtPeriodFrom:@@GVATReturnPymtPeriodTo
					
    Option                  : Vch CompanyOperations
	Option					: EL Voucher Number Warning Msg		: @@IsEditLogEnabled
	Option					: Non Stripes Form					: @@IsStockJrnl OR @@IsPhysStock

	Volatile   	    		: If NOT @@InvoiceInCreate Then No Else If @@IsSameClass Then If ($IsDefaultAllocEnabled:VoucherType:##SVVoucherType AND (##SVVoucherType != ##PreviousVoucherType)) Then $$RegenerateVoucher Else No Else $$RegenerateVoucher
    Keys            		: Change System Period

    Default Value   		: ""

	Control Ex				: Vch TaxDetailsModified Authorised	: NOT (@@IsMRPGoodsAllowStates OR @@RBVATTransferRightToUseControl) AND (@@IsVchTaxDetailsModified AND @@IsUserAuthorisedToOverride) 	: vVchSelectedIndex
	
	Fetch Object			: Godown						: ($ExciseUnitName:VoucherType:##SVVoucherType)	: TaxUnitName
	Fetch Object			: TaxUnit						: @@CMPExcisePrimaryGodown	: GSTApplicableDate, GSTRegistrationType, GSTRegNumber, IsEwayBillApplicable, EwayApplicableAmount, eWayBillApplicableDate, +
                                                                StateName, IsStateCessApplicable, StateCessApplicableDate, IsOthTerritoryAssessee

;;;;;;;;;;;;;

	Control     : LA_PaymethodSelect : Not ##LA_BillPaid and $$InCreateMode
	Print after Save	: if ##LA_PayTypeStr = "HOLD" then No else Yes
	No Confirm : Yes

	set	:	LA_MultiPayAccept	:	No
	Set	:	LA_BillPaid			:	No
	Set	:	LA_PayTypeStr		:	"CASH"
	
	set	:	CashTenderdAmountVar	:	""
	set	:	LA_POSMultiCashAmnt		:	""	
	set	:	LA_POSMultiBenefitAmnt	:	""

	set	:	LA_POSMultiCardType		:	""
	set	:	LA_POSMultiCardAmount	:	""
	set	:	LA_POSMultiCardNumber	:	""

    Horizontal Align: Left
    Vertical Align  : Top
	Width	: 100 % Page
	Height	: 100 % Page	
	
    Space Bottom    : 0

	SetAlways       : UseClassFmt 					: @@UseClassFmt
    Set Always		: POSSinglePayment		: If @@UseClassFmt Then No Else ##POSSinglePayment 
	Set             : InvoicingModeFlag : Yes
	
	
	Set				: SVViewName 		: If @@AccountingView then $$SysName:AcctgVchView Else +
										  If @@InventoryView then $$SysName:InvVchView Else +
										  If @@PayrollView	then $$SysName:PaySlipVchView Else +
										  If (@@MultiMfgrJrnlView OR @@IsJobMaterialVchs) then $$SysName:MulConsVchView Else +										  
										  If @@StkJrnlView then $$SysName:ConsVchView Else $$ShowMessage:"SVViewName variable is not set properly : Error is triggered from voucher.500"	

	SetAlways       : DSPTitleRefresh   : No
	Set Always      : SVAccAllocOnOrder : ##ICFGAccAllocOnOrder

	Volatile   	    : If NOT @@InvoiceInCreate Then No Else If @@IsSameClass Then No Else $$RegenerateVoucher

    Keys            : Change System Period,Disburse Auto Fill
    
    Default Value   : ""

    Key						: GSTVAT TaxAnalysis
	
	Set             : SVAccounts         		: ##AccountsVch OR ##PayrollVch 
    Set             : SVInventory          		: NOT ##AccountsVch AND NOT ##PayrollVch
    Set             : SVStatutory          		: No
	Set 			: SVTallyShop				: No
    Set				: SVVoucherOfType	        : @@TypeOfVoucherPrint	
	
	Option 			: DummyOption 				: (##SVClass = "x" OR ##SVClass != "x") OR (##AutoFillDone OR NOT ##AutoFillDone)
	Option			: VoucherAlterMode		    : $$InAlterMode
   	Option	: DispModeForUser : $$InDisplayMode AND NOT $$IsCmpOwner
	
;;;;;;;Screen form

	Set				:	VARVchDate		:	$$MachineDAte


	Option	:	LA_POS_CreateVch	:	Yes

;	On	:	Form Accept	:	($$IsEmpty:$Name:Ledger:#VCHPOSPartyContact) and (Not $$IsNotApplicable:#VCHPOSPartyContact)	:	Call	:	LA_POS_CreatePartyLed:#VCHPOSPartyContact
;	On	:	Form Accept	:	Yes:Form Accept

	On	:	Form Accept	:	yes	:	Call	:	UpdatePaidMethod:##LA_PayTypeStr	
	Switch	: VchTypeColor	  :  POS Invoice Color  : $$IsSales:##SVVoucherType 
	
[!Form: DispModeForUser]
	
	Local	: Button: Default      : Inactive: Yes
	
[!Form:LA_POS_CreateVch]

	Option	: Small Size Form
	
	Part	:	VCH Registration, VCH Status, VCH Status,+
				VCHTitle,VCHTitle2,VCHOptionalInfo,+
				LA_POS_VCHTitle,LA_POS_VCHTop,LA_POSBody

	Width	: 100 % page
	Height	: 100 % Page		
	Delete	: Bottom ToolBar Buttons		
	Local	:	Style	:	Default	:	Font	:	"Verdana"
	Background	:	White
	
	Local	:	Part	:	VCHTitle	:	Invisible	:	Yes
	Local	:	Part	:	VCHTitle2	:	Invisible	:	Yes

	[Part:LA_POS_VCHTitle]
			Line		: LA_POS_VCHTitle
			[Line:LA_POS_VCHTitle]
				Field:LA_POS_VCHTitle
				[Field:LA_POS_VCHTitle]
					    Use         : Short Name Field
						Color       : @@SV_VCHCOLOR
						Background  : @@SV_VCHTYPE
						Skip        : Yes
						Set as      : ##SVVoucherType
						Set always  : Yes
						Modifies    : SVVoucherType
						Storage     : Voucher Type Name
						Align       : Center
						Style		: Large Bold
						Full Width	:	Yes

	[Part: LA_POS_VCHTop]

		Left Part		: LA_POS_VCHTopLeft
		Right Part		: LA_POS_VCHTitleRight
		Border			: Thin Bottom
		Vertical		: No
		Space Top		: 0.15
		Space Bottom	:	0.15
		Local	:	Field	:	Default	:	Color	:	Royal Blue

;;;;;;;;;;;;;;;;;  Warehouse and Customer ;;;;;;;;;;;;;;

		[Part:LA_POS_VCHTopLeft]
			
			Part	:	LA_POSGodownInfo,POSPartyInfo
			Vertical	:	Yes

			Local	:	Part	:	POS PartyNameInfo	:	Add	:	Line	:After:	VCHPOSPartyContact	:	LA_POSPaymentMethod
				
			Local	:	Field	:	VCH POS PartyContact	:	Delete	:	Table
			Local	:	Field	:	VCH POS PartyContact	:	Delete	:	Sub Form
			Local	:	Line	:	VCH POS PartyContact	:	Local	:	Field	:	Simple Prompt	:	Set as	:	"Customer"
			Local	:	Line	:	VCH POS PartyContact	:	Local	:	Field	:	Simple Prompt	:	Width		: @@VCHLeftPromptWidth % Page
			Local	:	Field	:	VCH POS PartyContact	:	Width	:	100 MMS
			Local	:	Field	:	VCH POS PartyContact	:	Border	:	thin Box
			
			Local	:	Part	:	EI Price Level		:	Invisible	:	Yes
			Local	:	Part	:	POS PartyNameInfo	:	Delete		:	Width
			[Line:LA_POSPaymentMethod]
				Field	:	Medium Prompt,LA_POSPaymentMethod
				Local	:	Field	:	Medium Prompt	:	Set as	:	"Payment Method"
				Invisible	:	Not $$InDeveloperMode			
				
				[Field:LA_POSPaymentMethod]
					Use	:	Name Field
					Set Always	:	Yes
					Storage	:	LA_POSPaymentMethod
					
            [Part:LA_POSGodownInfo]

		        Part		: LA_POSGodown
				Invisible	: (NOT ($$IsMultiGodownOn OR @@IsSingaporeGSTEnabled)) 
				
				[Part: LA_POSGodown]
            
					Line	: LA_VCHPOSGodown								
					Local   : Line	: LA_VCHPOSGodown	: Local: Field: Default			: Inactive	: NOT $$IsMultiGodownOn 
					Local   : Line	: LA_VCHPOSGodown	: Local: Field: Short Prompt	: Width		: @@VCHLeftPromptWidth % Page

					 [Line: LA_VCHPOSGodown]

						Field                   : Short Prompt, LA_VCHPOSGodown

						Local                   : Field : Short Prompt  : Set as	:	"Warehouse"
						Invisible               : NOT $$IsMultiGodownOn
							
						[Field: LA_VCHPOSGodown]
							 
							Use             : Name Field
							Use				: Create Godown
							Set as          : If $$InAlterMode OR $$InDuplicateMode Then $$CollectionField:$GodownName:First:InventoryEntries Else ##POSGodown
							Table           : Godown, Create Master
							Keys            : Create Godown, Alter Godown
							Modifies        : POSGodown	: Yes
							Variable        : SV Godown
							Background		: white
							Border			: Thin Box
							Width	:	100 MMS
							
;;;;;;;;;;;;;;;;;;;;;;   Date and VChNo	;;;;;;;;;;;;;;;;;
							
		[Part:LA_POS_VCHTitleRight]
			Part	:LA_POS_VCHDate,LA_POS_VCHNo
			Vertical	:	Yes
			Space Top	:	0.15

			[Part:LA_POS_VCHDate]
				Line	:	VCH Date
;				Local	:	Field	:	Plain VCH Date	:	Set as	:	$$MachineDate
				Local	:	Line	:	VCH Date			:	local	:	Field:VCH Date:Add	:	Field	:	Before	:Plain VCH Date:MediumPrompt
				Local	:	Field	:	MediumPrompt		:	Set as	:	"Date"
				Local	:	Field	:	MediumPrompt		:	Width	:	30 MMS
				Local	:	Line	:	VCH Date			:	local	:	Field:VCH Date:Local:	Field	:	Plain VCH Date:Width	:	70 MMS
				
			[Part:LA_POS_VCHNo]

				Line	:	LA_POS_VCHNo
				Space Top	:	0.15
				
				[Line:LA_POS_VCHNo]

					Right Field	:	MediumPrompt, VCH Number, VchNumber DispSeries
					
					Local	:	Field	:	MediumPrompt	:	Set as	:	"POS Number"
					Local	:	Field	:	MediumPrompt	:	Width	:	30 MMS
					Local	:	Field	:	VCH Number		:	Border	:	Thin Box
					Local	:	Field	:	VCH Number		:	Width	:	70 MMS

;;;;;;;;;;;;;;;;;;;;  POS Main Body	;;;;;;;;;;;

[Part:LA_POSBody]
	
	Part	:	LA_POSBodyMain
	Vertical	:	Yes

	[Part:LA_POSBodyMain]

		Part	:	LA_POSInvLeft,LA_POSInvRight
		Vertical	:	No

		;;;;;;;;;;;;;;;;  Inventory Entries;;;;;;
		[Part:LA_POSInvLeft]
			
			Part	:	LA_POSBodyColTit,LA_POSInvDetails
			Bottom Part	:	LA_POSInvTotal
			Vertical	:	Yes
			

			[Part:LA_POSBodyColTit]
					
				Line:LA_POSBodyColTit
				Border	:	thin Bottom
				local	:	Field	:	Default	:	Color       : @@SV_VCHCOLOR
				Background  : @@SV_VCHTYPE

				[Line:LA_POSBodyColTit]

					Fields      : VCH ItemTitle
					Right Fields: VCHBilledQtyTitle,+ ;LA_POSStockBalTit,
								  VCHRateTitle, VCHRateUnitsTitle, +
								  VCHDiscTitle,VCHAmtTitle,LA_VCHDiscTitle,LA_VCHSubTaxTitle,LA_VCHSubTotalTitle
					
					Local	:	Field	:	VCHBilledQtyTitle	:	Delete	:	Inactive
					Local	:	Field	:	VCHBilledQtyTitle	:	Set as	:	"Quantity"
					Local	:	Field	:	Default	:	Style	:	Small Bold
					

					[Field:LA_VCHDiscTitle]
						Width       : @@VCHQtyWidth
						Setas       : $$LocaleString:"Discount"
						Style       : Small
						Align       : Centre
						Skip        : Yes
						Fixed       : Yes

					[Field:LA_VCHSubTaxTitle]
						Width       : @@VCHQtyWidth
						Setas       : $$LocaleString:"Tax"
						Style       : Small
						Align       : Centre
						Skip        : Yes
						Fixed       : Yes
						Invisible	:	Yes
						
					[Field:LA_VCHSubTotalTitle]
						Width       : @@VCHQtyWidth
						Setas       : $$LocaleString:"Sub Total"
						Style       : Small
						Align       : Centre
						Skip        : Yes
						Fixed       : Yes
						
			[Part:LA_POSInvDetails]
				
				Part:LA_POSInvDetailsInv,LA_CIAccInfo
				Total   : VCHValue, CIValue
				Total	: VCHBilledQty
				Border	:	thin Box
				Vertical:	Yes
				Local	:	Part	:	LA_CIAccInfo	:	Invisible	:	Yes

				[Part: LA_CIAccInfo]
						
					Lines       : CI AccDetails
					Repeat      : CI AccDetails : Ledger Entries
					Set as      : $$NumItems:ClassLedgerEntries +2
					Set always  : Yes

			[Part:LA_POSInvDetailsInv]

				Lines   : CI InvDetails;, EI InvSubTotal
				Repeat  : CI InvDetails : Inventory Entries
				BreakOn : $$IsSysName:$StockItemName       
				Vertical: Yes
				Scroll  : Vertical

				Local	:	Line	:	CI InvInfo	:	Explode	:	Default	:	No

				Local	:	Line	:	CI InvInfo	:	Add	:	Right Field	:	At End	:	LA_VCHDisc,LA_VCHSubTax,LA_VCHSubTotal
				Local	:	Field	:	Default	:	Border	:	Thin Box
				
				Local: Line: CI InvInfo : Add: Fields: After:VCHStockItem : VCHBATCH Godown
				Local: Line: CI InvInfo : Local: Field: VCHBATCH Godown   : Invisible:  Yes
				Local: Line: CI InvInfo : Local: Field: VCHBATCH Godown   : Inactive: No
				Local: Line: CI InvInfo : Local: Field: VCHStockItem 	  : Delete: Sub Form    : STKVCH BatchAllocations
				Local: Line: CI InvInfo : Local: Field: VCHStockItem 	  : Delete: Sub Form    : EI DescExplosion
				Local: Line: CI InvInfo : Local: Field: VCHBATCH Godown   : Add: Set as: if $$IsEmpty:#LA_VCHPOSGodown then "MainLocation" else #LA_VCHPOSGodown

							
				[Field:LA_VCHDisc]

					Use			: Amount Forex Field
					Style       : Small
					Align       : Centre
					Border      : Thin Right
					Storage		: LA_VCHDisc
					Set Always	: Yes
					Width       : @@VCHQtyWidth	
					Inactive    : $$IsEnd:$StockItemName OR @@NoBaseUnits

				[Field:LA_VCHSubTax]

					Use         : Amount Forex Field
					Style       : Small
					Align       : Centre
					Border      : Thin Right
					Storage		: LA_VCHSubTax
					Skip		: Yes
					Set Always	: Yes
					Width       : @@VCHQtyWidth
					Set as      : If $$IsEmpty:@transnature Then "" Else If ##vIsDebit Then $$AsDrAmt:@taxamt Else $$AsCrAmt:@taxamt
					taxamt      : $$ReportObject:$$CollectionFieldByKey:$VTAGCCInv_TaxAmount:@@GVATTaxAnalysisItemLedKey:VATGCCTaxAnalysisAlongWithItems
					transnature : $$ReportObject:$$CollectionFieldByKey:$VATGCCInv_TransNature:@@GVATTaxAnalysisItemLedKey:VATGCCTaxAnalysisAlongWithItems
					Inactive    : $$IsEnd:$StockItemName OR @@NoBaseUnits
					Read Only	: Yes
					Invisible	:	Yes

				[Field:LA_VCHSubTotal]
					
					Use			: Amount Forex Field
					Style       : Small
					Align       : Centre
					Border      : Thin Right
					Skip        : Yes
					Fixed       : Yes
					Storage		: LA_VCHSubTotal
					Width       : @@VCHQtyWidth
					Inactive    : $$IsEnd:$StockItemName OR @@NoBaseUnits
					Read Only	: Yes
					Set Always	:	yes
					Set as	:	$Amount - $LA_VCHDisc
				
	[Part:LA_POSInvTotal]
		
		Part	:	VCHPOSModeOfReceipts,Cust_VCHPOSModeOfReceipts
		Vertical	:	Yes

		Local	: Part	:	VCHPOSModeOfReceipts	:	Invisible	:	Yes

		Local	: Field :   VCHPOSInvoiceTotal 		: Set as	:	$$Total:vchValue +$$Total:CIValue
		Local	: Field :   VCHPOSInvoiceTotal 		: Storage 	: 	LA_POS_InvTotal

		Local	: Part	:	VCHPOSModeOfReceipts	: Local		:	Field	:	VCHPOSBankLedger	:	Set as	:	$POSBankLedgerClass:VoucherType:##SVVoucherType
		Local	: Part	:	VCHPOSModeOfReceipts	: Local		:	Field	:	VCHPOSCardLedger	:	Set as	:	$POSCardLedgerClass:VoucherType:##SVVoucherType
		Local	: Part	:	VCHPOSModeOfReceipts	: Local		:	Field	:	VCHPOSCashLedger	:	Set as	:	$POSCashLedgerClass:VoucherType:##SVVoucherType

[Part:Cust_VCHPOSModeOfReceipts]	

	Part	:	Cust_VCHTotalDiscAndRnd,LA_POSInvDetailsLed
	Vertical	:	No

	[Part:Cust_VCHTotalDiscAndRnd]
		Line	:	LA_POSInvTotalTitle,LA_POSInvTotal
		
		[Line:LA_POSInvTotalTitle]
			
				Field		:	LA_POSItemDiscTotal,LA_POSInvDisc,LA_POSDiscTotal,+
								LA_POSRoundOff
							
				Local	:	Field	:	Default	:	Type:	String
				Local	:	Field	:	Default	:	Delete	:	Storage
				Local	:	Field	:	Default	:	Align	:	Center
				Local	:	Field	:	Default	:	Style	:	Small Bold
				Local	:	Field	:	Default	:	Skip	:	yes
					
				Local	:	Field	:	LA_POSItemDiscTotal		:	Set as	:	"Item Disc"+$$NewLine+"Total"
				Local	:	Field	:	LA_POSInvDisc			:	Set as	:	"Inv." +$$NewLine+"Discount"
				Local	:	Field	:	LA_POSDiscTotal			:	Set as	:	"Total"++$$NewLine+"Discount"
				Local	:	Field	:	LA_POSRoundOff			:	Set as	:	"Round Off"
				
				Local	:	Field	:	Default					:	Line	:	2
				
		[Line:LA_POSInvTotal]

				Field	:	LA_POSItemDiscTotal,LA_POSInvDisc,LA_POSDiscTotal,+
							LA_POSRoundOff
				Local	:	Field	:	Default	:	Border	:	Thin Box
				Space Top	:	3 MMS
				

;;;;;;;;;;;;;;  Vat And Total	
	
[Part:LA_POSInvDetailsLed]

	Part	:	LA_POSInvTaxableAmount,LA_POSInvLedLeft,LA_POSInvGrandTotal
	Horizontal Align	:	Right
	Vertical	:	No
	Local	:	Field	:	Default	:	Border	:	Thin Box
	[Part:LA_POSInvTaxableAmount]

		Line:LA_POSInvTaxableAmountTit,LA_POSInvTaxableAmount
					
		[Line:LA_POSInvTaxableAmountTit]
			
			Field	:	LA_POSInvTotal
			Local	:	Field	:	LA_POSInvTotal	:	Set as	:	"Taxable" +$$NewLine+"Amount"			
			Local	:	Field	:	LA_POSInvTotal	:	Align	:	Center
			Local	:	Field	:	LA_POSInvTotal	:	Type	:	String
			Local	:	Field	:	LA_POSInvTotal	:	Line	:	2
			Local	:	Field	:	LA_POSInvTotal	:	Skip	:	Yes
			
							
		[Line:LA_POSInvTaxableAmount]
						
			Field	:	LA_POSInvTaxableAmount

			Local	:	Field	:	LA_POSInvTaxableAmount			:	Border		:	Thin Box
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Background	:	Dark Red
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Color		:	white
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Read Only	:	Yes
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Width		:	35 MMS
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Style		:	Large Bold
			Local	:	Field	:	LA_POSInvTaxableAmount			:	Set as		:	if @@isCreditNote then #EIValueTotal-#LA_POSInvDetailsLed else #VCHPOSInvoiceTotal-#LA_POSInvDetailsLed

	[Part:LA_POSInvLedLeft]
		
		Line:LA_POSInvDetailsLedTit,LA_POSInvDetailsLed
		Repeat:LA_POSInvDetailsLed	:	LedgerEntries
					
		[Line:LA_POSInvDetailsLedTit]
			
			Field	:	LA_POSInvTotal
			Local	:	Field	:	LA_POSInvTotal	:	Set as	:	"VAT" +$$NewLine+"Amount"			
			Local	:	Field	:	LA_POSInvTotal	:	Align	:	Center
			Local	:	Field	:	LA_POSInvTotal	:	Type	:	String
			Local	:	Field	:	LA_POSInvTotal	:	Line	:	2
			Local	:	Field	:	LA_POSInvTotal	:	Skip	:	Yes
			
							
		[Line:LA_POSInvDetailsLed]
						
			Field	:	LA_POSInvDetailsLed

			Local	:	Field	:	LA_POSInvDetailsLed			:	Border	:	Thin Box
			Local	:	Field	:	LA_POSInvDetailsLed			:	Background	:	Dark Red
			Local	:	Field	:	LA_POSInvDetailsLed			:	Color	:	white
			Local	:	Field	:	LA_POSInvDetailsLed			:	Read Only	:	Yes
			Local	:	Field	:	LA_POSInvDetailsLed			:	Width	:	35 MMS
			Local	:	Field	:	LA_POSInvDetailsLed			:	Style	:	Large Bold	
			Local	:	Field	:	LA_POSInvDetailsLed			:	Delete	:	Storage

			Remove if	:	Not $$IsSysNameEqual:VAT:$TaxType:Ledger:$LedgerName

			[Field:LA_POSInvDetailsLed]
				Use	:	CIValue
				Skip	:	No
				
[Part:LA_POSInvGrandTotal]
	Line	:	LA_POSInvGrandTotalTit,LA_POSInvGrandTotal
	
	[Line:LA_POSInvGrandTotalTit]
		
		Field	:	LA_POSInvTotal
		Local	:	Field	:	LA_POSInvTotal	:	Set as	:	"Grand" +$$NewLine+"Total"
		Local	:	Field	:	LA_POSInvTotal	:	Skip	:	Yes
		Local	:	Field	:	LA_POSInvTotal	:	Align	:	Center
		Local	:	Field	:	LA_POSInvTotal	:	Type	:	String
		Local	:	Field	:	LA_POSInvTotal	:	Line	:	2
							
		[Line:LA_POSInvGrandTotal]
						
			Field	:	VCHPOSInvoiceTotal
			
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Style	:	Large Bold					
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Read Only	:	Yes
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Width	:	35 MMS
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Color	:	white
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Background	:	Dark Red
			Local	:	Field	:	VCHPOSInvoiceTotal	:	Skip	:	No

		;;;;;;;;;;;;;;;;;;;  Invoice Total Fields	;;;;;;;;;

		[Field:LA_POSItemDiscTotal]

			Use			:	Amount Forex Field
			Set Always	:	Yes
			Set as		:	$$CollAmtTotal:InventoryEntries:$$Abs:$LA_VCHDisc
			Width		:	 MMS
			Format		:	"No Zero"
			Skip		:	Yes
			Style		:	Normal Bold
			Width		:	30 MMS
			Align		:	Right
			Read Only	:	Yes
			
		[Field:LA_POSInvDisc]
			Use	:	Amount Forex Field
			Set Always	:	Yes
			Width		:	30 MMS
			Format		:	"No Zero"
			Storage		:	LA_POSInvDisc
			Style		:	Normal Bold
			Align		:	Right

		[Field:LA_POSDiscTotal]

			Use	:	Amount Forex Field
			Set Always	:	Yes
			Width		:	30 MMS
			Storage     : 	LA_POSDiscTotal
			Set as		:	(#LA_POSItemDiscTotal + #LA_POSInvDisc)
			Style		:	Normal Bold
			Read Only	:	Yes
			
			On	:	Accept	:	Yes	:	Call	:	UpdateLedEntriss:$$Value:#LA_POSRoundOff
			On	:   Accept	:	Yes	:	Field Accept

		[Field:LA_POSRoundOff]
			Use	:	Amount Forex Field
			Set Always	:	Yes
			Width		:	25 MMS
			Style		:	Normal Bold
			Storage		:	LA_POSRoundOff
			
			On	:	Accept	:	Yes	:	Call	:	UpdateLedEntriss:#LA_POSDiscTotal:$$Value
			On	:   Accept	:	Yes	:	Field Accept
			
		[Field:LA_POSInvTaxableAmount]

			Use         : Amount Forex Field
			Set as      : ""
			Style		:	Normal Bold
			Set Always	:	yes	
			
			
		[Field:LA_POSInvTotal]
			Use         : Amount Forex Field
			Delete	:	Border
			Style	:	Large Bold
			Read Only	:	Yes
			Width	:	35 MMS
			Color	:	white
			Background	:	Dark Red
			Set Always	:	yes
			
	;;;;;;;;;;;; Number Pad	;;;;;;;;			
	[Part:LA_POSInvRight]
		Part	: LA_POSSettlements,LA_POSNum
		Vertical	:	Yes
		[Part:LA_POSSettlements]
			
			Part	:	LA_POSCashAndBenefit,LA_POSMultiMode,+
						LA_POSHoldAndRetake,LA_POSReturn
			Vertical: Yes
			Background: Pattens Blue
			Height	:	34% page
			Width	: 24 % Page
			Border	: Thin Box
			Horizontal Align: Center

			[Button: SettleMnetsBtns]
			
				Horizontal Align: Center
				Skip Forward: Yes
				Key Style: NumberKeyStyle
				Style	 : NumberKeyStyle
				Key Color: Black 

			[Part:LA_POSCashAndBenefit]

				Button	:	LA_POSCash,LA_POSBenefit

			[Part:LA_POSMultiMode]

				Button	:	LA_POSMultiPay

			[Part:LA_POSHoldAndRetake]

				Button:LA_POSHold,LA_POSReTake

			[Part:LA_POSReturn]

				Button	:LA_POSReturn

		[Part:LA_POSNum]
			Part	: LA_POS_Num7to9, LA_POS_Num4to6,+
					  LA_POS_Num3to1, LA_POS_Num0AndPoint,+
					  LA_POS_NumEnt,POS_Back
					  
			Vertical: Yes
			Background: Pattens Blue
			Height	: 50 % Page
			Width	: 24 % Page
			Border	: Thin Box
			Horizontal Align: Center

		[Button: NumberBadDesign]
			
			Horizontal Align: Center
			Height	: 8 % Page
			Width	: 8 % Page
			Skip Forward: Yes
			Key Style: NumberKeyStyle
			Style	 : NumberKeyStyle
			Key Color: Black 
			



	;;;;;;;;  Button Parts

			[Part: LA_POS_Num7to9]
		
				Button: POS_NUM7, POS_NUM8, POS_NUM9

			[Part: LA_POS_Num4to6]
		
				Button: POS_NUM6, POS_NUM5, POS_NUM4

			[Part: LA_POS_Num3to1]
		
				Button: POS_NUM3, POS_NUM2, POS_NUM1

			[Part: LA_POS_Num0andPoint]
				
				Button: POS_NUM0,POS_NUMPoint,POS_NUMBS
				

			[Part: LA_POS_NumEnt]
				
				Button: LA_POS_NumEnt
		
			[Part:POS_Back]
				Button	:	POS_BackInReport
				Local	:	Button	:	POS_BackInReport	:	Background	:	Dark Red
				Local	:	Button	:	POS_BackInReport	:	Width	: 23.5 % Page
				Local	:	Button	:	POS_BackInReport	:	Color	:	White
				Local	:	Button	:	POS_BackInReport	:	key Color	:	White
				Local	:	Button	:	POS_BackInReport	:	Horizontal Align	:	Center
				Local	:	Button	:	POS_BackInReport	:	Vertical Align		:	Center
