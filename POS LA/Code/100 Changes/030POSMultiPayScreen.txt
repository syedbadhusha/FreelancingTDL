
[Report:LA_POSMultiPayRep]
	Form	:	LA_POSMultiPayRep
	
	[Form:LA_POSMultiPayRep]

		Part	:	LA_POSMultiPayRep
		Width	:	60 % Page
		Control	:	LA_MsgWrongMulitPayAmount	:	##LA_POSMultiPayAmnt != ##LA_POSMultiCardAmount+##LA_POSMultiBenefitAmnt+##LA_POSMultiCashAmnt
		On	:	Form Accept	:	Yes	:	Set	:	LA_MultiPayAccept	:	yes
		Background	:	Royal Blue	
		Space Left	:	2 MMS
		Space Right	:	2 MMS

		[Part:LA_POSMultiPayRep]
			Part	:	LA_POSMultiPayRepWithTit,LA_POSMultiPayNumPad
			Vertical	:	No
			[Part:LA_POSMultiPayRepWithTit]
				Part	:	FormSubTitle,LA_POSMultiPayRepMn
				Vertical	:	yes
				Local	:	Part	:	FormSubTitle	:	Background	:	Dark Red
				Local	:	Part	:	FormSubTitle	:	Local	:	Field	:	FormSubTitle	:	Delete	:	Border
				Local	:	Part	:	FormSubTitle	:	Local	:	Field	:	FormSubTitle	:	Color	:	White
				Local	:	Part	:	FormSubTitle	:	Local	:	Field	:	FormSubTitle	:	Style	:	Large Bold
				Local	:	Part	:	FormSubTitle	:	Local	:	Line	:	FormSubTitle	:	Space Top	:	3.5 MMS
				Local	:	Part	:	FormSubTitle	:	Local	:	Field	:	FormSubTitle	:	Set as	:	"Multiple Payment Method For the Amount : " + $$LocaleString:##LA_POSMultiPayAmnt

			[Part:LA_POSMultiPayRepMn]
				
				Part	:	LA_POSMultiCreditOrDebit,LA_POSMultiBenefit,LA_POSMultiCash
				Vertical	:	Yes
				Border	:	thin Box

			[Part:LA_POSMultiPayNumPad]
				Part	:	LA_POSNum
				Vertical	:	No
;				Horizontal Align	:	Center
				Local	:	Part	:	LA_POSNum	:	Height	: 58 % Page
				Local	:	Part	:	LA_POSNum	:	Add	:	Part	:	Before	:	POS_Back:LA_POSMultiPayArrow
				
				[Part:LA_POSMultiPayArrow]
					Button	:	POS_Up,POS_Down
					Horizontal Align	:	Center	
					
;;;;;;;;;Credit Card Or Debit Card
			[Part:LA_POSMultiCreditOrDebit]
				Line	:	LA_POSMultiCardTypeTitle,LA_POSMultiCardType,+
							LA_POSMultiCardAmount,LA_POSMultiCardNumber
							
				Background	:	Dark Green

				Local	:	Line	:	Default	:	Height	:	7 MMS

				[Line:LA_POSMultiCardTypeTitle]
					Field	:	FormSubTitle
					Local	:	Field	:	FormSubTitle	:	Set as	:	"Credit Card/Debit Card"
					Local	:	Field	:	FormSubTitle	:	Color	:	White
						
				[Line:LA_POSMultiCardType]
					Field	:	Medium Prompt,LA_POSMultiCardType
					Local	:	Field	:	Medium Prompt	:	Set as	:	"Card Type"
					[Field:LA_POSMultiCardType]
						Use	:	Name Field
						Set Always	:	Yes
						Table	:	LA_POSMultiCardTypeCll,NotApplicable
						Show Table	:	Always
						Modifies	:	LA_POSMultiCardType	:	Yes
						Border	:	thin Box
						[Collection:LA_POSMultiCardTypeCll]

							Title	:	"Type of Card"
							List	:	"Credit Card","Debit Card"
							
				[Line:LA_POSMultiCardAmount]
					Field	:	Medium Prompt,LA_POSMultiCardAmount
					Local	:	Field	:	Medium Prompt	:	Set as	:	"Amount"
					[Field:LA_POSMultiCardAmount]
						Use	:	Amount Forex Field
						Set Always	:	Yes
						Modifies	:	LA_POSMultiCardAmount	:	Yes
						Border	:	thin Box
						Validate	:	Not $$IsEmpty:$$Value
						Set as		:	if $$IsNotApplicable:#LA_POSMultiCardType then "" else ##LA_POSMultiCardAmount
						Inactive	:	$$IsNotApplicable:#LA_POSMultiCardType
						
				[Line:LA_POSMultiCardNumber]
					Field	:	Medium Prompt,LA_POSMultiCardNumber
					Local	:	Field	:	Medium Prompt	:	Set as	:	"Card Number"
					[Field:LA_POSMultiCardNumber]
						Use	:	Name Field
						Set Always	:	Yes
						Modifies	:	LA_POSMultiCardNumber	:	Yes
						Border	:	thin Box
						Inactive	:	$$IsNotApplicable:#LA_POSMultiCardType
						
;;;;;;;;;Credit Card Or Debit Card End

			;;;;;;;;;;;;;  Benefit

		[Part:LA_POSMultiBenefit]
					Line	:	LA_POSMultiBenefitTit,LA_POSMultiBenefit
					Background	:	Blue
					Local	:	Line	:	Default	:	Height	:	7 MMS		
	
					[Line:LA_POSMultiBenefitTit]
						Field	:	FormSubTitle
						Local	:	Field	:	FormSubTitle	:	Set as	:	"Benefit Pay"
						Local	:	Field	:	FormSubTitle	:	Color	:	White
					
					[Line:LA_POSMultiBenefit]
						Field	:	Medium Prompt,LA_POSMultiBenefit
						Local	:	Field	:	Medium Prompt	:	Set as	:	"Amount"
						[Field:LA_POSMultiBenefit]
							Use	:	Amount Forex Field
							Set Always	:	Yes
							Modifies	:	LA_POSMultiBenefitAmnt	:	Yes
							Border	:	thin Box
							
							Set as	:	if $$IsEmpty:#LA_POSMultiCardAmount and $$IsEmpty:#LA_POSMultiCashAmnt +
										then ##LA_POSMultiPayAmnt else if (Not $$IsEmpty:#LA_POSMultiCardAmount and $$IsEmpty:LA_POSMultiCashAmnt) +
										then (##LA_POSMultiPayAmnt - #LA_POSMultiCardAmount) else +
										if (Not $$IsEmpty:#LA_POSMultiCashAmnt and $$IsEmpty:#LA_POSMultiCardAmount) then +
										(##LA_POSMultiPayAmnt - #LA_POSMultiCashAmnt) else if (Not $$IsEmpty:#LA_POSMultiCashAmnt +
										and Not $$IsEmpty:#LA_POSMultiCardAmount) then ##LA_POSMultiPayAmnt - (#LA_POSMultiCashAmnt+#LA_POSMultiCardAmount) +
										else ##LA_POSMultiBenefitAmnt
										
;							Inactive	:	##LA_POSMultiPayAmnt = #LA_POSMultiCardAmount or +
;											##LA_POSMultiPayAmnt = #LA_POSMultiCashAmnt or +
;											##LA_POSMultiPayAmnt = (#LA_POSMultiBenefit + #LA_POSMultiCashAmnt)
							
			[Part:LA_POSMultiCash]
					Line	:	LA_POSMultiCashTit,LA_POSMultiCashAmnt,+
								LA_POSMultiCashTend,LA_POSMultiCashBalance
					Background	:	Green

					[Line:LA_POSMultiCashTit]
						Field	:	FormSubTitle
						Local	:	Field	:	FormSubTitle	:	Set as	:	"Cash"
						Local	:	Field	:	FormSubTitle	:	Color	:	White
						
					[Line:LA_POSMultiCashAmnt]
						Field	:	Medium Prompt,LA_POSMultiCashAmnt
						Local	:	Field	:	Medium Prompt	:	Set as	:	"Cash Amount"

						[Field:LA_POSMultiCashAmnt]
							
							Use	:	Amount Forex Field
							Set Always	:	Yes
							Modifies	:	LA_POSMultiCashAmnt	:	Yes
							Border	:	thin Box

							Set as	:	if $$IsEmpty:#LA_POSMultiCardAmount and $$IsEmpty:#LA_POSMultiBenefit +
											then ##LA_POSMultiPayAmnt else if (Not $$IsEmpty:#LA_POSMultiCardAmount and $$IsEmpty:#LA_POSMultiBenefit) +
											then (##LA_POSMultiPayAmnt - #LA_POSMultiCardAmount) else +
											if (Not $$IsEmpty:#LA_POSMultiBenefit and $$IsEmpty:#LA_POSMultiCardAmount) then +
											(##LA_POSMultiPayAmnt - #LA_POSMultiBenefit) else if +
											(Not $$IsEmpty:#LA_POSMultiBenefit and Not $$IsEmpty:#LA_POSMultiCardAmount) then +
											(##LA_POSMultiPayAmnt - (#LA_POSMultiCardAmount+#LA_POSMultiBenefit)) else $$Value
							
							Inactive	:	##LA_POSMultiPayAmnt = #LA_POSMultiCardAmount or +
											##LA_POSMultiPayAmnt = #LA_POSMultiBenefit or +
											##LA_POSMultiPayAmnt = (#LA_POSMultiBenefit + #LA_POSMultiCardAmount)

					[Line:LA_POSMultiCashTend]
						Field	:	Medium Prompt,LA_POSMultiCashTend
						Local	:	Field	:	Medium Prompt	:	Set as	:	"Cash Tendered"
						[Field:LA_POSMultiCashTend]
							Use	:	Amount Field
							Set Always	:	Yes
							Modifies	:	CashTenderdAmountVar	:	Yes
							Border		:	thin Box
							Inactive	:	##LA_POSMultiPayAmnt = #LA_POSMultiCardAmount or +
											##LA_POSMultiPayAmnt = #LA_POSMultiBenefit or +
											##LA_POSMultiPayAmnt = (#LA_POSMultiBenefit + #LA_POSMultiCardAmount)

					[Line:LA_POSMultiCashBalance]
						Field	:	Medium Prompt,LA_POSMultiCashBalance
						Local	:	Field	:	Medium Prompt	:	Set as	:	"Balance"
						[Field:LA_POSMultiCashBalance]
							Use	:	Amount Field
							Set Always	:	Yes
							Set as		:	##CashTenderdAmountVar - ##LA_POSMultiCashAmnt 
							Border		:	thin Box
							Inactive	:	$$IsEmpty:#LA_POSMultiCashTend
							Skip	:	Yes
 	;;;;;;;;;;;;;;;; Cash Benefit End

