<DefType:Function>

[Function:POS_CreateLedger]
	Parameter		:	Name		:	String
	Parameter		:	Parent		:	String
	Parameter		:	IsVAT		:	Logical			:	No
	
	ifEmptName		:	Do If		:	($$IsEmpty:##Name or not $$IsEmpty:$Name:Ledger:##Name)		:	Return
	
	newLedger		:	New Object	:	Ledger
		setVName	:	Set Value	:	Name			:	##Name
		setVParent	:	Set Value	:	Parent			:	##Parent
		
		ifDuty				:	Do If		:	##Parent = $$GroupDuties			:	Set Value	:	TaxType				:	$$SysName:VAT
		ifDisc				:	Do If		:	##IsVAT								:	Set Value	:	VATDealerNature		:	"Discount";Set Value	:	VATApplicable	:	$$SysName:Applicable

		IsCostcenter 		: 	Set Value	:	IsCostCentresOn						:	No

		isVatforDisc 		: 	Do If		:	##IsVAT								:	Set Value	:	AppropriateFor		:	"VAT"
		isVatDiscApproTo	:	Do If		:	##IsVAT								:	Set Value	:	GSTAppropriateTo	:	"Goods and Services"
		
		IsSalesUpdateNature	:	Do If	:	##Parent = $$GroupSales	and @@IsGCCCountriesVATOn		:	Set Value	:	tempNature	:	"Domestic Taxable Supplies"
		SetVatList	:	If	:	##Parent = $$GroupSales	and @@IsGCCCountriesVATOn
			010	: Insert Collection Object	: VATDetails	: ##pCounter
			020	: 	Set Value	: Fromdate	: $VATApplDate:Company:##SVCurrentCompany
			030	: 	Set Value	: TaxType	: "Taxable"
			040	:	Set Value	: RateOfVAT	: $(Company, ##SVCurrentCompany).VATDetails[Last].RateofVAT
			045	:	Set Value	: StatNatureName	: "Domestic Taxable Supplies"
			900	: Set Target	: ..
			1000	:	End If

	saveLedger		:	Save Target
	
[Function:POS_BasicVCHTypeSetValue]
	Parameter		:	Name		:	String
	Parameter		:	Parent		:	String
	Parameter		:	IsPOS		:	String
	
	SVName			:	Set Value	:	Name					:	##Name
	SVParent		:	Set Value	:	Parent					:	##Parent
	SVIsActive		:	Set Value	:	Is Active				:	Yes
	SVUseForPOS		:	Set Value	:	UseForPOSInvoice		:	##IsPOS
				
	InsCllNumSrs	:	Insert Collection Object				:	VoucherNumberSeries
		SVNumNm		:	Set Value	:	Name					:	$$SysName:Default
		SVNumMtd	:	Set Value	:	NumberingMethod			:	"Automatic (Manual Override)";"Automatic"
		SVSNumMd	:	Set Value	:	NumberingSubMethod		:	"Auto Retain"
		SVPrvDup	:	Set Value	:	PreventDuplicates		:	Yes
	
[Function:POS_InsVCHTYPLedEntries]
	Parameter		:	Name		:	String
	Parameter		:	Method		:	String
	
	InsLedEntris	:	Insert Collection Object				:	LedgerEntriesList
		SVLedName	:	Set Value	:	Name					:	##Name
		SVLedMethod	:	Set Value	:	MethodType				:	##Method
		SVIsZero	:	Set Value	:	RemoveZeroEntries		:	No
	
[Function:POS_InsertVCHTypeClassLedgers]
	InsCLLedgerInv	:	Insert Collection Object				:	LedgerForInventoryList
		SVSalesLed	:	Set Value	:	Name					:	@@POS_SalesLedName
		SVClassRate	:	Set Value	:	ClassRate				:	100
		SVSalesZero	:	Set Value	:	RemoveZeroEntries		:	No
	
	InsVATLedEntries:	Call		:	POS_InsVCHTYPLedEntries	:	@@POS_VATLedName:($$SysName:VAT)	
	InsDiscLedEntris:	Call		:	POS_InsVCHTYPLedEntries	:	@@POS_DiscountLedName:($$SysName:AsUserDefined)
	InsRndLedEntries:	Call		:	POS_InsVCHTYPLedEntries	:	@@POS_RoundOffLedName:($$SysName:AsUserDefined)	
	
[Function:POS_VCHTypeClassPOSLedgersSetValue]
	SVCrdLed		:	Set Value	:	POSCardLedger			:	@@POS_DebitCardLedName
	SVCshLed		:	Set Value	:	POSCashLedger			:	@@POS_CashLedName
;	SVGftLed		:	Set Value	:	POSGiftLedger			:	@@POS_GiftLedName
	SVChqLed		:	Set Value	:	POSChequeLedger			:	@@POS_GiftLedName
	
[Function:POS_CreateVoucherType]
	Parameter		:	Name		:	String
	Parameter		:	Parent		:	String
	Parameter		:	IsPos		:	Logical					:	No
	
	ifEmptName		:	Do If		:	($$IsEmpty:##Name or not $$IsEmpty:$Name:VoucherType:##Name):	Return
	
	newVCHType		:	New Object	:	VoucherType
		setBasics	:	Call		:	POS_BasicVCHTypeSetValue:	##Name:##Parent:##IsPOS
		
		InsCllClass	:	Insert Collection Object				:	VoucherClassList
			SVClName:	Set Value	:	ClassName				:	@@POS_ClassName
			
			SVIsCrd	:	Set Value	:	POSEnableCardLedger		:	Yes
			SVIsCsh	:	Set Value	:	POSEnableCashLedger		:	Yes
			SVIsGift:	Set Value	:	POSEnableGiftLedger		:	No
			SVIsChq	:	Set Value	:	POSEnableChequeLedger	:	Yes
			SVIsAccL:	Set Value	:	POSEnableOnAccountLedger:	Yes
			
			ifPOS	:	Do If		:	(##IsPOS and $$IsSales:##Parent)	:	Call		:	POS_VCHTypeClassPOSLedgersSetValue
			
			InsCll	:	Call		:	POS_InsertVCHTypeClassLedgers
			
	saveVCHType		:	Save Target
	
[Function:POS_LedgerName]
	Parameter		:	MasterId	:	String
	
	Return			:	String
	
	000				:	Return		:	$$CollectionFieldByKey:$Name:##MasterId:POS_LedgerName